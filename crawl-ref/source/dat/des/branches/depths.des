##############################################################################
# depths.des: Depths entry vaults.
#             At time of writing, depths vaults are shared with D and
#             tend to go in variable/*.des.
##############################################################################

##########################################################################
# Depths entries.
# U:1 is intended to be a considerable change of pace from earlier (even
# if it isn't at all at time of writing); there is a single point of entry
# / exit between the two levels.
# These vaults hold the stair to U:1 and some enemies representative of
# later in the dungeon to make it clear what to expect.
##########################################################################

NAME: grunt_enter_depths_snipers
TAGS: no_pool_fixup
ORIENT: float
PLACE: D:$
KMONS: O = vault guard
KFEAT: O = enter_depths
MONS: centaur warrior, yaktaur
MAP
ccccccccc
c.+...+.c
c.c.O.c.c
c.c...c.c
c+cc+cc+c
c.w...w.c
c.w...w.c
c1w...w2c
c.w...w.c
c.w...w.c
ccc.@.ccc
ENDMAP

# The idea here is to keep the stone giant on the island until the player is
# ready to deal with it, and therefore proceed to the Depths.
NAME: grunt_enter_depths_giant_problem
# Don't generate lava monsters:
TAGS: no_monster_gen
ORIENT: float
PLACE: D:$
SUBST: L = l.
KMONS: O = patrolling stone giant
KFEAT: O = enter_depths
MAP
  ....@....
 ..LLLLLLL..
..LLlllllLL..
.LLlllllllLL.
.LlllLLLlllL.
.ll.L...LllL.
@l.l..O.LllL@
..llL...LllL.
.llllLLLlllL.
.LLlllllllLL.
..LLlllllLL..
 ..LLLLLLL..
  ....@....
ENDMAP

NAME: grunt_enter_depths_hoard
ORIENT: float
PLACE: D:$
KFEAT: O = enter_depths
MONS: fire dragon / ice dragon
MAP
  cccccc
 cc$$$$ccc
cc.......ccccc
cO..1........cc
cc.......cc...cc
 cc$$$$cccc.G..cc
  cccccccc.@.@..c
ENDMAP

NAME: grunt_enter_depths_magical_seal
ORIENT: float
PLACE: D:$
KMONS: O = ogre mage band / necromancer band
KFEAT: O = enter_depths
MONS: wizard / deep elf mage / orc sorcerer / kobold demonologist / \
      naga mage
MAP
  bbbbbbb
 bb..1..bb
bb.......bb
b..bb+bb..b
b..b...b..b
b.1b.O.b1.b
b..b...b..b
b..bbbbb..b
bb...1...bb
 bb.....bb
  bbb+bbb
ENDMAP

NAME: gammafunk_enter_depths_forms
ORIENT: float
PLACE: D:$
KFEAT: O = enter_depths
MONS: boggart band w:20 / giant orange brain, shapeshifter w:5 / rakshasa
MAP
  bbb
 bb.bb
 b...b   bbbb
 b...bb bb..bb
bb....bbb....bb
bO.1..+...2...+@
bb....bbb....bb
 b...bb bb..bb
 b...b   bbbb
 bb.bb
  bbb
ENDMAP

NAME: gammafunk_enter_depths_critters
ORIENT: float
PLACE: D:$
KFEAT: O = enter_depths
MONS: giant amoeba / brain worm / spiny worm w:5, slime creature band
NSUBST: . = 2:1 / *:.
MAP
 xxx xxxx xxx
xx xxx..xxx xx
x............x
xx....xx....xx
 x...xx....xx
 xx2.xx....x
xx...xxx...xx
x...xxxx....x
xxOxxxxxx..+x
 xxxx   xxxx@
ENDMAP

NAME: gammafunk_enter_depths_grave
ORIENT: float
TAGS: patrolling
PLACE: D:$
NSUBST: . = 1:2 / 4:1 / *:.
SUBST: . = .....--__tG
KMONS: t = w:2 col:darkgrey plant name:withered name_adjective \
            tile:mons_withered_plant / w:1 col:lightgrey plant name:withered  \
            name_adjective tile:mons_withered_plant
TILE: G = dngn_gravestone
COLOUR: G = white
FTILE: - = floor_pebble_brown
FTILE: _ = floor_pebble_darkgray
MONS:  w:2 guardian mummy / w:8 mummy / w:20 zombie / w:2 plague shambler
MONS:  skeletal warrior band
KFEAT: O = enter_depths
KMONS: O = vampire mage / necromancer band
: set_feature_name("granite_statue", "a gravestone")
MAP
  ....@....
 ...........
.............
.............
.............
.............
@.....O.....@
.............
.............
.............
.............
 ...........
  ....@....
ENDMAP

NAME: gammafunk_enter_depths_ironcross
ORIENT: float
TAGS: patrolling
PLACE: D:$
KFEAT: O = enter_depths
MONS: vault guard, ironbrand convoker, vault sentinel
SHUFFLE: 123
MAP
    vvvvv
    v...v
    v...v
    v.1.v
vvvvv...vvvvv
v...........v
v..2..O..3..v
v...........v
vvvvv...vvvvv
    v...v
    v...v
    v...v
    vv+vv
      @
ENDMAP
