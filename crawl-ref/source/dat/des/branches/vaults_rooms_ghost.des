################################################################################
# vaults_rooms_ghost.des: Ghost subvaults for the Vaults.
#
# Content: These are vault rooms that place a player ghost. All vaults here
# should call the function vaults_ghost_setup(). In addition to setting other
# standard tags for ghost vaults, this function will add the vaults_ghost tag
# that these vaults must have so they'll follow the global chance for placing a
# ghost vault on a level.
#
# Do not use other tag selectors like vaults_room and vaults_hard in these
# vaults, however any of the other standard vault room tags for orientation
# etc. are fine. Set a different WEIGHT to make a specific vault more or less
# common.
################################################################################

: require("dlua/ghost.lua")

NAME:   advil_vaults_ghost_guards
KITEM:  12 = superb_item / star_item
KMONS:  1 = vault guard / ironbrand convoker / ironheart preserver / \
            vault warden
KMONS:  2 = vault warden
KMONS:  3 = player ghost
SHUFFLE: 13
: vaults_ghost_setup(_G)
MAP
........
........
..nnnn..
..n31n..
..n12n..
..n==n..
........
........
ENDMAP

# based on st_lines
NAME:   advil_vaults_ghost_lines
KITEM:  123 = superb_item / star_item
KMONS:  1 = vault guard / ironbrand convoker / ironheart preserver / \
            vault warden
KMONS:  2 = vault warden
KMONS:  3 = player ghost
SHUFFLE: 1239
SUBST: 9 = 98
: vaults_ghost_setup(_G)
MAP
........
nnn==nnn
..=92=..
..=31=..
nnn==nnn
........
ENDMAP

NAME:  gammafunk_vaults_ghost_grave
TAGS:  vaults_orient_w
KITEM: O = superb_item / star_item
KMONS: p = withered plant
KMONS: O = player ghost
SUBST: de = |*, t : tp
TILE:  G = dngn_gravestone
TILE:  t = dngn_tree_dead
: set_feature_name("granite_statue", "a gravestone")
FTILE:  -|*%$OGt = floor_pebble_brown / floor_pebble_darkgray
: vaults_ghost_setup(_G)
MAP
..x.....
..x.....
..n.dt..
..=.OGt.
..n.et..
..x.....
..x.....
ENDMAP

NAME:  gammafunk_vaults_ghost_split
KMONS: O = player ghost
SUBST:  defg = |*
NSUBST: D = 2=0 / 2=0. / 9 / 2=9. / 8 / 8. / .
SUBST: c : bcv, p : b., q : b., b : bcv
: vaults_ghost_setup(_G)
MAP
.............
.............
..xxxnnnxxx..
..nD..D..Dx..
..n..qpq..x..
..=.Dpqpe.n..
..=...OgdDn..
..=.Dpqpf.n..
..n..qpq..x..
..nD..D..Dx..
..xxxnnnxxx..
.............
.............
ENDMAP

NAME:    gammafunk_vaults_ghost_cemetery
TAGS:    vaults_orient_s
KMONS:   DEF = player ghost
KMONS:   p = withered plant
# 1/12 chance for 5 ghosts, 1/6 for 4, 1/4 for 3, 1/2 for 2.
SHUFFLE: DdqEerFfs / DdqZyzFfs / DdqZyzFfs / DdqEerZyz / DdqEerZyz / \
         DdqEerZyz / DdqZyzZyz / DdqZyzZyz / DdqZyzZyz / DdqZyzZyz / \
         DdqZyzZyz / DdqZyzZyz
# 1.5 items per ghost, upgrading quality with depth.
NSUBST:  d = 2=|* / |*--, e = |* / |*--, f = 2=|* / |*--
SUBST:   qrs = G, Z = -, y = p, z = V, t : tp
TILE:    G = dngn_gravestone
TILE:    t = dngn_tree_dead
FTILE:   -|*%$DEFpGt = floor_pebble_brown / floor_pebble_darkgray
: set_feature_name("granite_statue", "a gravestone")
: vaults_ghost_setup(_G)
MAP
qd--ere--dq
dD---E---Dd
-----------
-----------
f---------f
sF-------Fs
f---------f
xxnn===nnxx
...........
...........
ENDMAP

NAME:   gammafunk_vaults_ghost_necromancy
TAGS:   vaults_orient_w
KMONS:  O = player ghost
MONS:   simulacrum place:Vaults:5 / spectre place:Vaults:5, lich
SUBST:  defg = |*, h = *, i = %$, j = -
NSUBST: 1 = 12 / 1, - = 3=1 / 6=1- / -
FTILE:  -|*%$O12G = floor_pebble_brown / floor_pebble_darkgray
: vaults_ghost_setup(_G)
MAP
..xxxxxxx
..n1x1x1x
..n---igx
..=---exx
..=---dOx
..=---fxx
..n---jhx
..n1x1x1x
..xxxxxxx
ENDMAP

NAME:   gammafunk_vaults_ghost_spectres
TAGS:   vaults_orient_w
KMONS:  O = player ghost
MONS:   wraith / shadow
MONS:   freezing wraith / shadow wraith / phantasmal warrior / flayed ghost w:5
MONS:   eidolon, revenant / curse skull
SUBST:  defg = |*, h = *, i = %$
NSUBST: D = 2 / 3 / 2=3- / 4-, - = 2=1 / 2=1- / 2 / 3=2- / -
FTILE:  -|*%$O1234G = floor_pebble_brown / floor_pebble_darkgray
: vaults_ghost_setup(_G)
MAP
..xxxxxxx
..nDxDxDx
..n----gx
..=---exx
..=--idOx
..=---fxx
..n----hx
..nDxDxDx
..xxxxxxx
ENDMAP

NAME:   gammafunk_vaults_ghost_fury_of_okawaru
TAGS:   vaults_orient_s
KFEAT:  O = altar_okawaru
KMONS:  O = player ghost
MONS:   centaur warrior / tengu warrior / orc knight / naga warrior / \
        merfolk impaler / merfolk javelineer / deep elf knight / \
        deep elf archer
MONS:   minotaur / stone giant / orc warlord
MONS:   fire giant / frost giant / deep elf blademaster / \
        deep elf master archer / titan
SUBST:  defgh = |*, i = *%, j = %$..
NSUBST: - = 3=1 / 4=1- / 2=2 / 2=2- / 2=3- / 3--- / -
: vaults_ghost_setup(_G)
MAP
GvvvvvvvG
-igvOvhj-
---edf---
-vG---Gv-
---------
-vG---Gv-
---------
nnn===nnn
.........
.........
ENDMAP

NAME:   ebering_vaults_ghost_inner_flame
TAGS:   vaults_orient_w
KPROP:  xn'O = w:5 bloody / nothing
KMONS:  O = player_ghost
KITEM:  O = scroll of immolation ident:all
NSUBST: ' = |* / |*$ / 998 / dF / 3=d. / F. / .
MARKER: F = lua:fog_machine { cloud_type = "black smoke", \
            pow_min = 10000, pow_max = 10000, delay = 1, \
            size = 1, walk_dist = 0, start_clouds = 1 }
: dgn.delayed_decay(_G, "d", "chunk")
: vaults_ghost_setup(_G)
MAP
..x....
..x''..
..n'''.
..='O'.
..n'''.
..x''..
..x....
ENDMAP
