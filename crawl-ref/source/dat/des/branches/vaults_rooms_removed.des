################################################################
#  vaults_rooms_removed
#
#  These rooms have all been removed at least for now until
#  review or fixing.
#
#   - 1x1 and 4x4 vaults. 4x4 ones could be reinstated (they
#     might help link together sections of the maze layout
#     where there it runs out of room for bigger one) but
#     the 1x1s seem unneccesary.
#     Except that they're used as subvaults by some of the 7x7
#     layouts so actually they still need to be available.
#     But they're not tagged with vaults_room so won't be
#     used on their own.
#
#   - Stuff that doesn't fit with the current design. Right
#     now we don't need vaults with pre-defined walls and
#     it's a fair bit of editing to fix them all
#
#   - Portal / branch / trove entries - these can be placed by the
#     standard dungeon routines when needed, they can be
#     converted into standard ones if wanted. Some appeared to be just
#     duplicates of existing ones anyway.
#
#   - Some larger subvault rooms seem to just be tying together the
#     smaller 7x7 rooms. The layout code can now produce this
#     sort of layout itself. Some had interesting ideas e.g.
#     the guarded subvault but we can just let this happen naturally
#     since monsters get placed in the level anyway.
#
#   - The slightly more complex "very large" vaults with subvaults. These
#     are nice and detailed and a lot of work has gone in
#     so they should definitely be used somehow but right now
#     I want to avoid the special casing required to get
#     them to place properly.
#
# - mumra

#
#  vaults_standard_loot
#
#  This function substitutes loot types for greater randomness.
#

{{
function vaults_standard_loot(e)
  e.subst("| = *|||")
  e.subst("* = %%%******|")
  e.subst("% = ....$%%%%*")
end
}}

#
#  vaults_numbered_gold_items
#
#  This function sets the next items (normally d-g) to have a
#    small amount of gold.  The quantities are:
#      -> d:  1- 5 gold
#      -> e:  6-10 gold
#      -> f: 11-15 gold
#      -> g: 16-20 gold
#

{{
function vaults_numbered_gold_items(e)
  e.item("gold q:1  / gold q:2  / gold q:3  / gold q:4  / gold q:5")
  e.item("gold q:6  / gold q:7  / gold q:8  / gold q:9  / gold q:10")
  e.item("gold q:11 / gold q:12 / gold q:13 / gold q:14 / gold q:15")
  e.item("gold q:16 / gold q:17 / gold q:18 / gold q:19 / gold q:20")
end
}}

#
#  vaults_ornamental_pool
#
#  This function randomly replaces water with some obstruction.
#    Using this standard function allows all the rooms to have
#    the same probability of each.  It also would allow other
#    cell values to be added, such as slippery ice if it ever
#    gets added.
#
#  This function does not activate the no_pool_fixup tag, but
#    does mark the water as no_monster_gen.
#
#  This function currently has a chance of the following:
#      -> deep water    (most common)
#      -> shallow water
#      -> lava
#      -> known spiderwebs   (rare)
#      -> unknown spiderwebs (very rare)
#

{{
function vaults_ornamental_pool(e)
  local replace_with = crawl.random2(60)
  if replace_with < 46 then
    e.subst("w : w:30 W:10 l:6")
    e.kmask("wWl = no_monster_gen")
    e.kfeat("w = w")
    e.kfeat("W = W")
    e.kfeat("l = l")
  elseif replace_with < 50 then
    e.subst("w = ww.")
    if crawl.random2(4) < 1 then
      e.kfeat("w : web trap")
    else
      e.kfeat("w : known web trap")
    end
  else
    -- weight 10
    e.subst("w : .")
  end
end
}}



################################################################
#  1x1 subvaults (closets)
#
#  There are fewer types of of these than for other size groups.
#    Specifically, there are no doors, no ???_facing_??? or
#    ???_dead_end_??? subvaults, and no subvaults containing 3
#    staircases.
#



########
#  Free-spinning 1x1 subvaults (closets)
#

NAME:     vaults_room_1_spin_empty_default
TAGS:     vaults_room_1_spin_empty allow_dup no_monster_gen no_item_gen transparent
MAP
.
ENDMAP

NAME:     vaults_room_1_spin_encounter_default
TAGS:     vaults_room_1_spin_encounter allow_dup no_monster_gen no_item_gen transparent
MAP
0
ENDMAP

NAME:     vaults_room_1_spin_dangerous_default
TAGS:     vaults_room_1_spin_dangerous allow_dup no_monster_gen no_item_gen transparent
MAP
9
ENDMAP

NAME:     vaults_room_1_spin_loot_default
TAGS:     vaults_room_1_spin_loot allow_dup no_monster_gen no_item_gen transparent
: vaults_standard_loot(_G)
MAP
%
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_spin_stairs1_default
TAGS:     vaults_room_1_spin_up1 vaults_room_1_spin_down1 transparent
MAP
{
ENDMAP

NAME:     vaults_room_1_spin_crypt_default
TAGS:     vaults_room_1_spin_crypt allow_dup no_monster_gen no_item_gen transparent
KFEAT:    O = enter_crypt
MAP
O
ENDMAP

NAME:     vaults_room_1_spin_blade_default
TAGS:     vaults_room_1_spin_blade allow_dup no_monster_gen no_item_gen transparent
KFEAT:    O = enter_hall_of_blades
MAP
O
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_1_spin_bazaar_subvault
TAGS:     vaults_room_1_spin_bazaar allow_dup no_monster_gen no_item_gen transparent
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
B
ENDMAP

NAME:     vaults_room_1_spin_lab_default
TAGS:     vaults_room_1_spin_lab allow_dup no_monster_gen no_item_gen transparent
SUBVAULT: B : uniq_lab
SUBST:    B = .
MAP
B
ENDMAP

NAME:     vaults_room_1_spin_trove_default
TAGS:     vaults_room_1_spin_trove allow_dup no_monster_gen no_item_gen transparent
SUBVAULT: B : chance_trove
SUBST:    B = .
MAP
B
ENDMAP

# not used because ziggurats are D only
# too small for a ziggurat entry, so empty
NAME:     vaults_room_1_spin_ziggurat_dummy
TAGS:     vaults_room_1_spin_ziggurat allow_dup transparent
MAP
.
ENDMAP



########
#  City-layout 1x1 subvaults (closets)
#

NAME:     vaults_room_1_city_empty_4_doors
TAGS:     vaults_room_1_city_empty allow_dup transparent
MAP
x+x
+.+
x+x
ENDMAP

NAME:     vaults_room_1_city_empty_statues
TAGS:     vaults_room_1_city_empty allow_dup transparent
MAP
G.G
...
G.G
ENDMAP

NAME:     vaults_room_1_city_empty_1_statue
TAGS:     vaults_room_1_city_empty allow_dup transparent
WEIGHT:   20
MAP
...
.G.
...
ENDMAP

NAME:     vaults_room_1_city_empty_1_pool
TAGS:     vaults_room_1_city_empty allow_dup no_pool_fixup transparent
: vaults_ornamental_pool(_G)
MAP
www
www
www
ENDMAP

NAME:     vaults_room_1_city_encounter_4_doors
TAGS:     vaults_room_1_city_encounter allow_dup transparent
MAP
x+x
+0+
x+x
ENDMAP

NAME:     vaults_room_1_city_encounter_statues
TAGS:     vaults_room_1_city_encounter allow_dup transparent
MAP
G.G
.0.
G.G
ENDMAP

NAME:     vaults_room_1_city_dangerous_4_doors
TAGS:     vaults_room_1_city_dangerous allow_dup transparent
MAP
x+x
+9+
x+x
ENDMAP

NAME:     vaults_room_1_city_dangerous_statue_guards
TAGS:     vaults_room_1_city_dangerous allow_dup transparent
NSUBST:   0 = 2:0 / 1:0. / *:.
MAP
000
0G0
000
ENDMAP

NAME:     vaults_room_1_city_loot_4_doors
TAGS:     vaults_room_1_city_loot allow_dup transparent
: vaults_standard_loot(_G)
MAP
x+x
+%+
x+x
ENDMAP

NAME:     vaults_room_1_city_loot_statues
TAGS:     vaults_room_1_city_loot allow_dup transparent
: vaults_standard_loot(_G)
MAP
G.G
.%.
G.G
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_city_stairs1_open
TAGS:     vaults_room_1_city_up1 transparent vaults_room_1_city_down1 allow_dup
TAGS:
WEIGHT:   20
NSUBST:   . = 1:0. / *:~

MAP
...
.{.
...
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_city_stairs1_4_doors
TAGS:     vaults_room_1_city_up1
TAGS:     vaults_room_1_city_down1 allow_dup transparent

MAP
x+x
+{+
x+x
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_city_stairs1_statues
TAGS:     vaults_room_1_city_up1
TAGS:     vaults_room_1_city_down1 allow_dup transparent
SUBST:    . = .:10 ~:1

MAP
G.G
.{.
G.G
ENDMAP





################################################################
#  4x4 subvaults
#

########
#  Doors for 4x4 subvaults
#

NAME:     vaults_door_4
TAGS:     vaults_door_4 allow_dup transparent
SUBST:    - : x+
MAP
+-
ENDMAP



########
#  Free-spinning 4x4 subvaults
#

NAME:     vaults_room_4_spin_empty_default
TAGS:     vaults_room_4_spin_empty allow_dup
SUBST:    G : xG...
WEIGHT:   50
MAP
G..G
....
....
G..G
ENDMAP

NAME:     vaults_room_4_spin_empty_pool
TAGS:     vaults_room_4_spin_empty allow_dup no_pool_fixup
: vaults_ornamental_pool(_G)
MAP
....
.ww.
.ww.
....
ENDMAP

NAME:     vaults_room_4_spin_empty_ring
TAGS:     vaults_room_4_spin_empty allow_dup
MAP
....
.xx.
.xx.
....
ENDMAP

NAME:     vaults_room_4_spin_empty_feature
TAGS:     vaults_room_4_spin_empty allow_dup
SUBST:    G : xG...
SUBST:    ! = SCT^
SUBST:    T = TTTUVV
KFEAT:    S = any shop
KFEAT:    ^ = known zot trap
MAP
G..G
.!..
....
G..G
ENDMAP

NAME:     vaults_room_4_spin_encounter_default
TAGS:     vaults_room_4_spin_encounter allow_dup no_monster_gen
NSUBST:   0 = 1:0 / *:.
MAP
....
.00.
.00.
....
ENDMAP

NAME:     vaults_room_4_spin_dangerous_default
TAGS:     vaults_room_4_spin_dangerous allow_dup no_monster_gen
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
: vaults_standard_loot(_G)
MAP
q..q
.pp.
.pp.
q..q
ENDMAP

NAME:     vaults_room_4_spin_loot_default
TAGS:     vaults_room_4_spin_loot allow_dup no_monster_gen
NSUBST:   % = 4:% / *:.
: vaults_standard_loot(_G)
MAP
%..%
.%%.
.%%.
%..%
ENDMAP

# 18 + 26 + 40 + 15 = average 99 gold
NAME:     vaults_room_4_spin_loot_gold
TAGS:     vaults_room_4_spin_loot allow_dup no_monster_gen no_item_gen
NSUBST:   $ = 1:g / 2:f / 5:e / 5:d / *:.
: vaults_numbered_gold_items(_G)
MAP
$$$$
$$$$
$$$$
$$$$
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_spin_stairs1_statues
TAGS:     vaults_room_4_spin_up1 vaults_room_4_spin_down1 allow_dup
SUBST:    G : xG...

MAP
G..G
.{..
....
G..G
ENDMAP

NAME:     vaults_room_4_spin_up3_center
TAGS:     vaults_room_4_spin_up3 allow_dup
SUBST:    G : xG...
SHUFFLE:  {([<
SUBST:    < = <.
MAP
G..G
.{(.
.[<.
G..G
ENDMAP

NAME:     vaults_room_4_spin_down3_center
TAGS:     vaults_room_4_spin_down3 allow_dup
SUBST:    G : xG...
SHUFFLE:  })]>
SUBST:    > = >.
MAP
G..G
.}).
.]>.
G..G
ENDMAP

NAME:     vaults_room_4_spin_secret_statues
TAGS:     vaults_room_4_spin_secret allow_dup no_monster_gen no_item_gen
SUBST:    G : xG%%%
NSUBST:   % = 8:% / *:.
: vaults_standard_loot(_G)

MAP
G%%G
%{.%
%.9%
G%%G
ENDMAP

NAME:     vaults_room_4_spin_secret_ring
TAGS:     vaults_room_4_spin_secret allow_dup no_monster_gen no_item_gen
SUBST:    w : xw
NSUBST:   9 = 1:9 / *:*

: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
{..9
.ww.
.ww.
9..9
ENDMAP

NAME:     vaults_room_4_spin_crypt_default
TAGS:     vaults_room_4_spin_crypt allow_dup
NSUBST:   O = 1:O / 1:1 / *:1.
MONS:     place:Crypt:1
KFEAT:    O = enter_crypt
FTILE:    O1. = floor_crypt
MAP
....
.OO.
.OO.
....
ENDMAP

NAME:     vaults_room_4_spin_crypt_grunt
TAGS:     vaults_room_4_spin_crypt allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_crypt
NSUBST:   8 = 2:1 / *:2
MONS:     guardian mummy, mummy
MAP
8..8
.Od.
.ef.
8..8
ENDMAP

NAME:     vaults_room_4_spin_blade_corners
TAGS:     vaults_room_4_spin_blade allow_dup
NSUBST:   O = 1:O / 1:1 / *:1.
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
FTILE:    xO1. = floor_hall
MAP
x..x
.OO.
.OO.
x..x
ENDMAP

NAME:     vaults_room_4_spin_blade_grunt
TAGS:     vaults_room_4_spin_blade allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_hall_of_blades
NSUBST:   8 = 2:1 / *:.
MONS:     dancing weapon
MAP
8..8
.Od.
.ef.
8..8
ENDMAP

########
#  4x4 subvaults with a facing direction
#

NAME:     vaults_room_4_facing_empty_default
TAGS:     vaults_room_4_facing_empty allow_dup no_monster_gen
SUBST:    w : wWl
SUBVAULT: A : vaults_door_4
MAP
....
....A
....A
....
ENDMAP

NAME:     vaults_room_4_facing_empty_statues_pool
TAGS:     vaults_room_4_facing_empty allow_dup no_pool_fixup
SUBVAULT: A : vaults_door_4
: vaults_ornamental_pool(_G)
MAP
G...
.ww.A
.ww.A
G...
ENDMAP

NAME:     vaults_room_4_facing_encounter_default
TAGS:     vaults_room_4_facing_encounter allow_dup no_monster_gen
SUBST:    y = 0:2 %:1 .:20
NSUBST:   . = 1:0 / *:.
SUBVAULT: A : vaults_door_4
: vaults_standard_loot(_G)
MAP
y...
y...A
y...A
y...
ENDMAP

NAME:     vaults_room_4_facing_dangerous_default
TAGS:     vaults_room_4_facing_dangerous allow_dup no_monster_gen
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_standard_loot(_G)
MAP
pq..
pq..A
pq..A
pq..
ENDMAP

NAME:     vaults_room_4_facing_loot_default
TAGS:     vaults_room_4_facing_loot allow_dup no_monster_gen
SUBST:    G : G%
NSUBST:   % = 4:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_standard_loot(_G)
MAP
G%.x
%%%.A
%%%.A
G%.x
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_facing_stairs1_open
TAGS:     vaults_room_4_facing_up1 vaults_room_4_facing_down1 allow_dup
SUBVAULT: A : vaults_door_4

MAP
{...
....A
....A
....
ENDMAP

NAME:     vaults_room_4_facing_up3_open
TAGS:     vaults_room_4_facing_up3 allow_dup
SHUFFLE:  {([<
SUBST:    < = <.
SUBVAULT: A : vaults_door_4
MAP
{..(
....A
....A
[..<
ENDMAP

NAME:     vaults_room_4_facing_down3_open
TAGS:     vaults_room_4_facing_down3 allow_dup
SHUFFLE:  })]>
SUBST:    > = >.
SUBVAULT: A : vaults_door_4
MAP
}..)
....A
....A
]..>
ENDMAP

NAME:     vaults_room_4_facing_crypt_grunt
TAGS:     vaults_room_4_facing_crypt allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_crypt
NSUBST:   8 = 2:1 / *:2
MONS:     guardian mummy, mummy
SUBVAULT: A : vaults_door_4
MAP
.8..
8Od.A
8ef.A
.8..
ENDMAP

NAME:     vaults_room_4_facing_blade_grunt
TAGS:     vaults_room_4_facing_blade allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_hall_of_blades
NSUBST:   8 = 2:1 / *:.
MONS:     dancing weapon
SUBVAULT: A : vaults_door_4
MAP
.8..
8Od.A
8ef.A
.8..
ENDMAP



########
#  Dead-end 4x4 subvaults
#

NAME:     vaults_room_4_dead_end_empty_narrow
TAGS:     vaults_room_4_dead_end_empty allow_dup
SUBVAULT: A : vaults_door_4
MAP
xxxx
....A
....A
xxxx
ENDMAP

NAME:     vaults_room_4_dead_end_empty_statues_ring
TAGS:     vaults_room_4_dead_end_empty allow_dup
SUBST:    I : G.
SUBVAULT: A : vaults_door_4
MAP
xG.I
G...A
G...A
xG.I
ENDMAP

NAME:     vaults_room_4_dead_end_empty_flooded
TAGS:     vaults_room_4_dead_end_empty allow_dup
SUBST:    W = w.
KMASK:    w = no_monster_gen
SUBVAULT: A : vaults_door_4
MAP
wwwW
wwW.A
wwW.A
wwwW
ENDMAP

NAME:     vaults_room_4_dead_end_empty_statues_checkerboard
TAGS:     vaults_room_4_dead_end_empty allow_dup
WEIGHT:   5
NSUBST:   % = 3:% / *:.
: vaults_standard_loot(_G)
MAP
.G.G
G.G.+
.G.Gx
G.G.
ENDMAP

NAME:     vaults_room_4_dead_end_encounter_statues
TAGS:     vaults_room_4_dead_end_encounter allow_dup
SUBST:    I : G.
NSUBST:   0 = 1:0 / *:.
SUBVAULT: A : vaults_door_4
MAP
xG0I
G00.A
G00.A
xG0I
ENDMAP

NAME:     vaults_room_4_dead_end_dangerous_flooded
TAGS:     vaults_room_4_dead_end_dangerous allow_dup no_pool_fixup
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
wwqq
wpp.A
wpp.A
wwqq
ENDMAP

NAME:     vaults_room_4_dead_end_loot_flooded
TAGS:     vaults_room_4_dead_end_loot allow_dup no_pool_fixup
NSUBST:   % = 4:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
ww%%
w%%.A
w%%.A
ww%%
ENDMAP

NAME:     vaults_room_4_dead_end_loot_statues_checkerboard
TAGS:     vaults_room_4_dead_end_loot allow_dup
WEIGHT:   5
NSUBST:   % = 4:% / *:.
: vaults_standard_loot(_G)
MAP
%G%G
G%G.+
%G%Gx
G%G%
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_dead_end_stairs1_open
TAGS:     vaults_room_4_dead_end_up1 vaults_room_4_dead_end_down1 allow_dup
WEIGHT:   20
NSUBST:   { = 1:{ / *:.
SUBVAULT: A : vaults_door_4

MAP
{{..
{{..A
{{..A
{{..
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_dead_end_stairs1_narrow
TAGS:     vaults_room_4_dead_end_up1 vaults_room_4_dead_end_down1 allow_dup

MAP
xxxx
xxxxx
{...+
xxxx
ENDMAP

NAME:     vaults_room_4_dead_end_up3_open
TAGS:     vaults_room_4_dead_end_up3 allow_dup
SHUFFLE:  {([<
SUBST:    < = <.
SUBVAULT: A : vaults_door_4
MAP
{...
(...A
[...A
<...
ENDMAP

NAME:     vaults_room_4_dead_end_down3_open
TAGS:     vaults_room_4_dead_end_down3 allow_dup
SHUFFLE:  })]>
SUBST:    > = >.
SUBVAULT: A : vaults_door_4
MAP
}...
)...A
]...A
>...
ENDMAP



########
#  City-layout 4x4 subvaults
#

NAME:     vaults_room_4_city_empty_cross
TAGS:     vaults_room_4_city_empty allow_dup
WEIGHT:   20
MAP
xx++xx
xx..xx
+....+
+....+
xx..xx
xx++xx
ENDMAP

NAME:     vaults_room_4_city_empty_water
TAGS:     vaults_room_4_city_empty allow_dup no_pool_fixup
SHUFFLE:  GHI/pqr
SHUFFLE:  GH
SUBST:    G : G.
SUBST:    HI = .
SUBST:    pqr = w
: vaults_ornamental_pool(_G)
MAP
GHIIHG
HwwwwH
IwwwwI
IwwwwI
HwwwwH
GHIIHG
ENDMAP

NAME:     vaults_room_4_city_encounter_corners
TAGS:     vaults_room_4_city_encounter allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
NSUBST:   0 = 1:% / *:0
: vaults_standard_loot(_G)
MAP
xpxxqx
p0xx0q
xxxxxx
xxxxxx
r0xx0s
xrxxsx
ENDMAP

NAME:     vaults_room_4_city_dangerous_corners
TAGS:     vaults_room_4_city_dangerous allow_dup
NSUBST:   A = 1:+ / *:-
NSUBST:   B = 1:+ / *:-
NSUBST:   C = 1:+ / *:-
NSUBST:   D = 1:+ / *:-
SUBST:    - = x:20 +:1
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
: vaults_standard_loot(_G)
MAP
xAxxBx
A.qq.B
xqppqx
xqppqx
C.qq.D
xCxxDx
ENDMAP

NAME:     vaults_room_4_city_loot_corners
TAGS:     vaults_room_4_city_loot allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
NSUBST:   % = 3:% / *:.
: vaults_standard_loot(_G)
MAP
xpxxqx
p%GG%q
xG%%Gx
xG%%Gx
r%GG%s
xrxxsx
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_city_stairs1_corner
TAGS:     vaults_room_4_city_up1 vaults_room_4_city_down1 allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
NSUBST:   { = 1:{ / *0
SUBST:    0 = .:6 0:3 %:1
: vaults_standard_loot(_G)

MAP
xpxxqx
p{xx{q
xxxxxx
xxxxxx
r{xx{s
xrxxsx
ENDMAP

NAME:     vaults_room_4_city_stairs1_C
TAGS:     vaults_room_4_city_up1 vaults_room_4_city_down1 allow_dup

MAP
xxxxxx
x{...x
xxxx.x
xxxx.x
+....x
xxxxxx
ENDMAP

NAME:     vaults_room_4_city_up3_open
TAGS:     vaults_room_4_city_up3 allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
SHUFFLE:  {([<
SUBST:    < = <.0
MAP
xpxxqx
p{xx(q
xxxxxx
xxxxxx
r[xx<s
xrxxsx
ENDMAP

NAME:     vaults_room_4_city_down3_open
TAGS:     vaults_room_4_city_down3 allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
SHUFFLE:  })]>
SUBST:    > = >.0
MAP
xpxxqx
p}xx)q
xxxxxx
xxxxxx
r]xx>s
xrxxsx
ENDMAP

#
#  A set of rooms that use subvaults for walls.
#
#  The total wall weight is 100.
#

NAME:     vaults_room_4_city_empty_subring
TAGS:     vaults_room_4_city_empty allow_dup
SUBVAULT: A : vaults_room_4_city_subring
MAP
AAAAAA
AA..AA
A....A
A....A
AA..AA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_empty_subring_fungus
TAGS:     vaults_room_4_city_empty allow_dup
WEIGHT:   5
MONS:     fungus / nothing w:30,  fungus / nothing
SUBVAULT: A : vaults_room_4_city_subring
MAP
AAAAAA
AA11AA
A1221A
A1221A
AA11AA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_empty_subring_water
TAGS:     vaults_room_4_city_empty allow_dup no_pool_fixup
WEIGHT:   5
SUBVAULT: A : vaults_room_4_city_subring
: vaults_ornamental_pool(_G)
MAP
AAAAAA
AAwwAA
AwwwwA
AwwwwA
AAwwAA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_encounter_subring
TAGS:     vaults_room_4_city_encounter allow_dup
NSUBST:   0 = 1:0 / *:.
SUBVAULT: A : vaults_room_4_city_subring
MAP
AAAAAA
AA..AA
A.00.A
A.00.A
AA..AA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_dangerous_subring
TAGS:     vaults_room_4_city_dangerous allow_dup
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_room_4_city_subring
: vaults_standard_loot(_G)
MAP
AAAAAA
AAqqAA
AqppqA
AqppqA
AAqqAA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_subring_wall
TAGS:     vaults_room_4_city_subring allow_dup transparent
WEIGHT:   60
SHUFFLE:  ABCD
SUBVAULT: A : vaults_door_4
: if crawl.random2(6) < 1 then
:   subvault("B : vaults_door_4")
: else
:   subst("B = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("C : vaults_door_4")
: else
:   subst("C = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("D : vaults_door_4")
: else
:   subst("D = x")
: end
MAP
.xAAx.
xx  xx
B    C
B    C
xx  xx
.xDDx.
ENDMAP

NAME:     vaults_room_4_city_subring_statue_ring
TAGS:     vaults_room_4_city_subring allow_dup transparent
WEIGHT:   20
MAP
..GG..
.G  G.
G    G
G    G
.G  G.
..GG..
ENDMAP

NAME:     vaults_room_4_city_subring_8_statues
TAGS:     vaults_room_4_city_subring allow_dup transparent
WEIGHT:   10
MAP
.G..G.
G.  .G
.    .
.    .
G.  .G
.G..G.
ENDMAP

NAME:     vaults_room_4_city_subring_statue_corners
TAGS:     vaults_room_4_city_subring allow_dup transparent
WEIGHT:   10
MAP
Gx++xG
xx  xx
+    +
+    +
xx  xx
Gx++xG
ENDMAP



######################
# Portal entries etc.

# This is just "minmay_crypt_up_simple_box" with new tags
NAME:     vaults_room_7_spin_crypt_default
TAGS:     vaults_room_7_spin_crypt allow_dup no_monster_gen
MONS:     place:Crypt:1
KFEAT:    O = enter_crypt
MAP
.......
.cc.cc.
.c1c1c.
..cOc..
.c1c1c.
.cc.cc.
.......
ENDMAP

NAME:     vaults_room_7_spin_crypt_grunt
TAGS:     vaults_room_7_spin_crypt allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:2
MONS:     guardian mummy, mummy
KFEAT:    O = enter_crypt
MAP
.......
.1...1.
.......
...O...
.......
.1...1.
.......
ENDMAP

NAME:     vaults_room_7_spin_blade_pillars
TAGS:     vaults_room_7_spin_blade allow_dup no_monster_gen
SUBST:    y : x...
SUBST:    1 = 1.
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
FTILE:    cxyO1. = floor_hall
MAP
xy...yx
y..c..y
..1.1..
.c.O.c.
..1.1..
y..c..y
xy...yx
ENDMAP

NAME:     vaults_room_7_spin_blade_grunt
TAGS:     vaults_room_7_spin_blade allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:.
MONS:     dancing weapon
KFEAT:    O = enter_hall_of_blades
MAP
.......
.1...1.
.......
...O...
.......
.1...1.
.......
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_7_spin_bazaar_subvault
TAGS:     vaults_room_7_spin_bazaar allow_dup no_monster_gen no_item_gen
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
.......
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.......
ENDMAP

NAME:     vaults_room_7_spin_lab_hunger
TAGS:     vaults_room_7_spin_lab allow_dup no_monster_gen no_item_gen
NSUBST:   B = 1:B / 2:1.. / *:.
MONS:     hungry ghost
FTILE:    xB1. = floor_labyrinth
RTILE:    x = wall_lab_rock
SUBVAULT: B : uniq_lab
SUBST:    B = .
MAP
Bx.....
.x.xxx.
....Bx.
.xxxxx.
.xB....
.xxx.xx
Bx....B
ENDMAP

NAME:     vaults_room_7_spin_trove_subvault
TAGS:     vaults_room_7_spin_trove allow_dup no_monster_gen no_item_gen
SUBVAULT: B : chance_trove
SUBST:    B = .
MAP
.......
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.......
ENDMAP
