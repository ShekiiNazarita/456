###################################
# Rejected vaults
#
# I don't want to outright delete stuff without a review but I don't see any point in these vaults or any way to fix them!
# Some of them were outright breaking the layout, others are just not at all interesting.
# mumra

NAME:     vaults_room_7_spin_empty_cross
TAGS:     vaults_room vaults_empty allow_dup
SUBST:    ~ = ~:1 .:20
MAP
xx...xx
xxx~xxx
.xx~xx.
.~~.~~.
.xx~xx.
xxx~xxx
xx...xx
ENDMAP

# Thi was creating blocked-off map areas and failing validation
NAME:     vaults_room_7_spin_dangerous_boxed
TAGS:     vaults_room vaults_hard allow_dup
SUBST:    - : xxm++...
KPROP:    _ = no_rtele_into
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
SUBVAULT: E : vaults_door_7
MAP
_-...-_
-xEEEx-
.D...B.
.D.9.B.
.D...B.
-xCCCx-
_-...-_
ENDMAP

# More traps

NAME:     vaults_room_7_spin_loot_cross
TAGS:     vaults_room allow_dup no_monster_gen
: vaults_standard_loot(_G)
SUBST:    ~ = ~:1 .:10
MAP
xx...xx
xxx~xxx
.xx~xx.
.~~*~~.
.xx~xx.
xxx~xxx
xx...xx
ENDMAP

# Veto culprit
NAME:     vaults_room_7_spin_loot_boxed
TAGS:     vaults_room allow_dup
SUBST:    - : xxm++...
KPROP:    _ = no_rtele_into
NSUBST:   % = 4:% /.
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
SUBVAULT: E : vaults_door_7
: vaults_standard_loot(_G)
MAP
_-...-_
-xEEEx-
.D%%%B.
.D%%%B.
.D%%%B.
-xCCCx-
_-...-_
ENDMAP

# We can use a code vault for stuff like this if there is any point
# average 13 + 24 + 114 = 151 gold
NAME:     vaults_room_7_spin_loot_gold
TAGS:     vaults_room allow_dup no_monster_gen no_item_gen
NSUBST:   $ = 1:f / 3:e / 38:d / *:.
: vaults_numbered_gold_items(_G)
MAP
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
ENDMAP
# This is for both entry and exit stairs
NAME:     vaults_room_7_spin_stairs1_boxed
TAGS:     vaults_room vaults_room allow_dup
SUBST:    - : xxm++...
KPROP:    _ = no_rtele_into
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
SUBVAULT: E : vaults_door_7

MAP
_-...-_
-xEEEx-
.D...B.
.D.{.B.
.D...B.
-xCCCx-
_-...-_
ENDMAP

# Stairs can easily be placed in any sized empty room
NAME:     vaults_room_7_spin_up3_grunt_open
TAGS:     vaults_room allow_dup no_monster_gen no_item_gen
SHUFFLE:  {([<
SUBST:    < = .
MAP
.......
.{...[.
.......
.......
.......
.(...<.
.......
ENDMAP


# The following ones include their own (non-standard-shaped) outer wall which isn't supported yet
# Although they could just be used as normal floating minifaults

NAME:     vaults_room_7_city_empty_cross_statues
TAGS:     vaults_room vaults_empty allow_dup
WEIGHT:   20
SHUFFLE:  GHIJK
SUBST:    HIJK = .
SUBVAULT: A : vaults_room_7_city_cross_wall
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.G...G.A
A.I.H.I.A
A.G...G.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_empty_cross_water
TAGS:     vaults_room vaults_empty allow_dup
SUBST:    W = w.
SUBVAULT: A : vaults_room_7_city_cross_wall
MAP
AAAAAAAAA
AAAWWWAAA
AAAwwwAAA
AWwwwwwWA
AWwwwwwWA
AWwwwwwWA
AAAwwwAAA
AAAWWWAAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_encounter_cross
TAGS:     vaults_room allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SUBVAULT: A : vaults_room_7_city_cross_wall
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.G...G.A
A.I.0.I.A
A.G...G.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_dangerous_cross
TAGS:     vaults_room vaults_hard allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SHUFFLE:  pq
NSUBST:   p = 1:9 / 1:%. / *:.
NSUBST:   q = 3:0 / 1:% / *:.
SUBVAULT: A : vaults_room_7_city_cross_wall
: vaults_standard_loot(_G)
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.GpppG.A
A.IpppI.A
A.GpppG.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

# average 36 + 39 + 16 + 6 = 99 gold
NAME:     vaults_room_7_city_loot_glass_walls
TAGS:     vaults_room allow_dup
SUBST:    G : G..
NSUBST:   = = 1:+ / *:m
NSUBST:   $ = 2:g / 3:f / 2:e / *:d
SUBVAULT: A : vaults_door_7
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
: vaults_numbered_gold_items(_G)
MAP
xxxBBBxxx
xGm...mGx
xmm===mmx
C.=$$$=.A
C.=$$$=.A
C.=$$$=.A
xmm===mmx
xGm...mGx
xxxDDDxxx
ENDMAP

NAME:     vaults_room_7_city_up3_glass_box
TAGS:     vaults_room allow_dup
SUBST:    G : G__
KPROP:    _ = no_rtele_into
SHUFFLE:  pqrs
SUBST:    p = +
SUBST:    qrs = m
SHUFFLE:  {}
SUBST:    } = .
NSUBST:   { = 1:{ / 1:( / 1:[ / *:<
SUBVAULT: A : vaults_door_7
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
MAP
xxxBBBxxx
xGm...mGx
xmxqqqxmx
C.r{}{p.A
C.r}.}p.A
C.r{}{p.A
xmxsssxmx
xGm...mGx
xxxDDDxxx
ENDMAP

NAME:     vaults_room_7_city_down3_cross
TAGS:     vaults_room allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SHUFFLE:  {}
SUBST:    { = .
NSUBST:   } = 1:} / 1:) / 1:] / *:>
SUBVAULT: A : vaults_room_7_city_cross_wall
: vaults_standard_loot(_G)
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.G}{}G.A
A.I{.{I.A
A.G}{}G.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

# a subvault used for the walls of cross-shaped 7x7 city rooms
NAME:     vaults_room_7_city_cross_wall
TAGS:     vaults_room_7_city_cross_wall allow_dup
SUBST:    G : G....
SHUFFLE:  ABCD
SUBVAULT: A : vaults_door_7
: if crawl.random2(6) < 1 then
:   subvault("B : vaults_door_7")
: else
:   subst("B = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("C : vaults_door_7")
: else
:   subst("C = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("D : vaults_door_7")
: else
:   subst("D = x")
: end
MAP
G.xBBBx.G
..x   x..
xxx   xxx
C       A
C       A
C       A
xxx   xxx
..x   x..
G.xDDDx.G
ENDMAP

# Isolated center
NAME:     vaults_room_7_spin_loot_water_ring
TAGS:     vaults_room allow_dup no_pool_fixup no_monster_gen
NSUBST:   % = 6:% / *:_
KPROP:    %_ = no_rtele_into
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
.......
.wwwww.
.w%%%w.
.w%%%w.
.w%%%w.
.wwwww.
.......
ENDMAP


NAME:     vaults_room_7_city_empty_statue_rings
TAGS:     vaults_room vaults_empty allow_dup
WEIGHT:   20
SHUFFLE:  GI
SUBST:    I : G.
SUBST:    H = G.
SUBVAULT: A : vaults_room_7_city_statue_rings
MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A...A.A
A.A.G.A.A
A.A...A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_encounter_statue_rings
TAGS:     vaults_room allow_dup
SUBST:    G = G0
NSUBST:   0 = 1:0 / *:.
SUBVAULT: A : vaults_room_7_city_statue_rings
MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A000A.A
A.A0G0A.A
A.A000A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP


NAME:     vaults_room_7_city_loot_statue_rings
TAGS:     vaults_room allow_dup no_pool_fixup
WEIGHT:   5
SUBVAULT: A : vaults_room_7_city_statue_rings
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
AAAAAAAAA
AAwwwwwAA
AwwAAAwwA
AwA...AwA
AwA.*.AwA
AwA...AwA
AwwAAAwwA
AAwwwwwAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_up1_statue_rings
TAGS:     vaults_room allow_dup
WEIGHT:   5
SUBVAULT: A : vaults_room_7_city_statue_rings

MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A...A.A
A.A.{.A.A
A.A...A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP
# a subvault used for the double statue rings
NAME:     vaults_room_7_city_statue_rings
TAGS:     vaults_room_7_city_statue_rings allow_dup
SHUFFLE:  GH
SHUFFLE:  IJ
SHUFFLE:  GI
SUBST:    I : G.
SUBST:    HJ = .
MAP
..HG.GH..
.G     G.
H  IJI  H
G I   I G
. J   J .
G I   I G
H  IJI  H
.G     G.
..HG.GH..
ENDMAP
