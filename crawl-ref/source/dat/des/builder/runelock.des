##########################################################################
# Vaults holding the stair from D:14 to D:15.
# D:15 is intended to be a considerable change of pace from earlier (even
# if it isn't at all at time of writing); there is a single point of entry
# / exit between the two levels.
# These vaults hold the stair to D:15 and some enemies representative of
# later in the dungeon to make it clear what to expect.
##########################################################################

NAME: grunt_runelock_snipers
TAGS: no_pool_fixup
ORIENT: float
PLACE: D:14
KMONS: O = vault guard
KFEAT: O = }
MONS: centaur warrior, yaktaur
MAP
ccccccccc
c.+...+.c
c.c.O.c.c
c.c...c.c
c+cc+cc+c
c.w...w.c
c.w...w.c
c1w...w2c
c.w...w.c
c.w...w.c
ccc.@.ccc
ENDMAP

# The idea here is to keep the stone giant on the island until the player is
# ready to deal with it, and therefore proceed to the rest of D.
NAME: grunt_runelock_giant_problem
# Don't generate lava monsters:
TAGS: no_monster_gen
ORIENT: float
PLACE: D:14
SUBST: L = l.
KMONS: O = patrolling stone giant
KFEAT: O = }
MAP
  ....@....
 ..LLLLLLL..
..LLlllllLL..
.LLlllllllLL.
.LlllLLLlllL.
.ll.L...LllL.
@l.l..O.LllL@
..llL...LllL.
.llllLLLlllL.
.LLlllllllLL.
..LLlllllLL..
 ..LLLLLLL..
  ....@....
ENDMAP

NAME: grunt_runelock_hoard
ORIENT: float
PLACE: D:14
MONS: dragon / ice dragon
MAP
  cccccc
 cc$$$$ccc
cc.......ccccc
c}..1........cc
cc.......cc...cc
 cc$$$$cccc.G..cc
  cccccccc.@.@..c
ENDMAP

NAME: grunt_runelock_magical_seal
ORIENT: float
PLACE: D:14
KMONS: O = ogre mage band / necromancer band
KFEAT: O = }
MONS: wizard / deep elf mage / orc sorcerer / kobold demonologist / \
      naga mage
MAP
  bbbbbbb
 bb..1..bb
bb.......bb
b..bb+bb..b
b..b...b..b
b.1b.O.b1.b
b..b...b..b
b..bbbbb..b
bb...1...bb
 bb.....bb
  bbb+bbb
ENDMAP

NAME: gammafunk_runelock_forms
ORIENT: float
PLACE: D:14
MONS: boggart band w:20 / giant orange brain, shapeshifter w:5 / rakshasa
MAP
  bbb
 bb.bb
 b...b   bbbb
 b...bb bb..bb
bb....bbb....bb
b}.1..+...2...+@
bb....bbb....bb
 b...bb bb..bb
 b...b   bbbb
 bb.bb
  bbb
ENDMAP

NAME: gammafunk_runelock_critters
ORIENT: float
PLACE: D:14
MONS: giant amoeba / brain worm / spiny worm w:5, slime creature band
NSUBST: . = 2:1 / *:.
MAP
 xxx xxxx xxx
xx xxx..xxx xx
x............x
xx....xx....xx
 x...xx....xx
 xx2.xx....x
xx...xxx...xx
x...xxxx....x
xx}xxxxxx..+x
 xxxx   xxxx@
ENDMAP

NAME: gammafunk_runelock_grave
ORIENT: float
TAGS: patrolling
PLACE: D:14
NSUBST: . = 1:2 / 4:1 / *:.
SUBST: . = .....--__tG
KMONS: t = w:2 col:darkgrey plant name:withered name_adjective \
            tile:mons_withered_plant / w:1 col:lightgrey plant name:withered  \
            name_adjective tile:mons_withered_plant
TILE: G = dngn_gravestone
COLOUR: G = white
FTILE: - = floor_pebble_brown
FTILE: _ = floor_pebble_darkgray
MONS:  w:2 guardian mummy / w:8 mummy / w:20 zombie / w:2 plague shambler
MONS:  skeletal warrior band
KFEAT: O = }
KMONS: O = vampire mage / necromancer band
: set_feature_name("granite_statue", "a gravestone")
MAP
  ....@....
 ...........
.............
.............
.............
.............
@.....O.....@
.............
.............
.............
.............
 ...........
  ....@....
ENDMAP

NAME: gammafunk_runelock_ironcross
ORIENT: float
TAGS: patrolling
PLACE: D:14
MONS: vault guard, ironbrand convoker, vault sentinel
SHUFFLE: 123
MAP
    vvvvv
    v...v
    v...v
    v.1.v
vvvvv...vvvvv
v...........v
v..2..}..3..v
v...........v
vvvvv...vvvvv
    v...v
    v...v
    v...v
    vv+vv
      @
ENDMAP
