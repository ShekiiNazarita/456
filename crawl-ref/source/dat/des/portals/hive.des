###############################################################################
# hive.des - Hive entry vaults and layouts.
#
# The hive offers food for the player, but they will have to face berserking
# bees and grab as much as they can before the larvae eat all the spoils and
# transform into bees themselves.
###############################################################################


###############################################################################
# Hive entries

# Utility functions

lua {{

function place_hive_portal(e, perm)
  e.kfeat("O = enter_portal_vault")
  e.lua_marker('O', hive_portal())
end

function hive_portal()
  local d = "You hear a manifold buzzing. The waxy walls of this " ..
  "place are dripping with honey, but the larvae are hungry, and the queen " ..
  "bee will not tolerate intruders."
  return one_way_stair { desc = 'gateway to a hive', desc_long = d,
                           dst = 'new_hive', floor = 'stone_arch' }
end

function hive_message(e)
  crawl.mpr("You enter an furiously buzzing hive!")
end

function hive_milestone(e)
  crawl.mark_milestone("br.enter", "entered a Hive.", true)
end

function random_hive_colour()
  local colours = {"blue", "red", "lightblue", "magenta", "green"}
  return colours[crawl.random2(#colours) + 1]
end

function fixup_hive()
  dgn.fixup_stairs("stone_arch", "exit_portal_vault")

  if (dgn.get_floor_colour() == "black") then
    dgn.change_floor_colour(random_hive_colour())
  end

  if (dgn.get_rock_colour() == "black") then
    dgn.change_rock_colour("yellow")
  end

  dgn.change_floor_tile("floor_hive")
  dgn.change_rock_tile("wall_hive")
end

dgn.set_lt_callback("new_hive", "fixup_hive")

function hive_epilogue(e)
  e.hook("epilogue",
         function ()
           hive_message(_G)
           hive_milestone(_G)
         end)
end

}}

default-depth: D:11-16

##############################################################################
# Hive entries
##############################################################################


##############################################################################
# Standard hive
#
NAME: david_new_hive_1
TAGS: uniq_new_hive patrolling no_monster_gen
MONS: queen bee, killer bee, killer bee larva
KITEM: R = w:2 honeycomb / w:1 royal jelly
ORIENT: float
CHANCE: 5%
: place_hive_portal(_G)
MAP
............
....aaaa....
..a2a2aaaa..
..aaRa3a2a..
.aa2aRa2aaa.
.a3aOa1aRa2.
.aa3aRaRa2a.
.aaa2a2a3aa.
..a3aRa2aa..
...aa2aa2a..
....aaaa....
............
ENDMAP

##############################################################################
# Hive monsters near stair
#
NAME: david_new_hive_2
TAGS: uniq_new_hive patrolling no_monster_gen
MONS: nothing / killer bee / killer bee larva
MONS: queen bee
ORIENT: float
CHANCE: 5%
: place_hive_portal(_G)
MAP
1111..@.
1211....
1111....
............1
@.....O.....@
   ..........
   ......1
   .......
   1@.....
ENDMAP

#############################################################################
# Cooperation Not Competition (Rot)
#
NAME:    new_hive_david_3
TAGS:    uniq_new_hive patrolling no_monster_gen
ORIENT:  float
ITEM:    honeycomb, royal jelly
MONS:    killer bee larva / killer bee, queen bee
MONS:    worker ant / soldier ant, queen ant
MONS:    yellow wasp / nothing, red wasp / yellow wasp
MONS:    bumblebee
SHUFFLE: XY, VW, 34/56
NSUBST:  O = 1:O / *:de.
SUBST:   V==, W=a, X==, Y=a
SUBST:   ? = a .:20
SUBST:   1 : 1 7:2
SUBST:   7 = 7 .
CHANCE: 5%
: place_hive_portal(_G)
MAP
   .......   ...............
   ..??...   ..??aaa?aaa???.
...?aaa?..   .?aaOaaaaOaa?..
.?aaa.aa??.....?aa=.aa=Oaa?.
.aa333.aaa.......a?a11aaa?..
.aaO43..=...T...a.1aOa1.a?..
.?a.33.aaa......aa.1a2a1aa?.
.?aa.3aa??.....aaaVa1a1aYaa.
..?aaaa?.......?aaOW..aXOaa.
   ????.........?aaaaaaaaa?.
   ......     ...??aaa?a??
   ......     ............
ENDMAP

##############################################################################
# Bears Won't Share
#
NAME:    erik_new_hive_1
TAGS:    uniq_new_hive patrolling no_monster_gen
ORIENT:  float
MONS:    polar bear / grizzly bear w:200 / black bear w:100
MONS:    killer bee, queen bee, killer bee larva
ITEM:    honeycomb, royal jelly
SUBST:   ? = a.
SUBST:   ; = 1:1 .
SHUFFLE: deO
CHANCE: 5%
: place_hive_portal(_G)
MAP
  ...............
 ;;;;;...?aaa???.
.;;;;;..aaa2aaaaa..
.;;;;;.?a2e4d342a?.
.;;;;;.?a4?2?4O4a?.
.;;;;;..?a42?a2a...
.;;;;;..?aaaaaa?
 ;;;;;....????..
  ..............
ENDMAP

##############################################################################
# Bucolic
#
NAME:   erik_new_hive_2
TAGS:   uniq_new_hive patrolling no_monster_gen
ORIENT: float
MONS:   plant, killer bee
SUBST:  1 = 1.
SUBST:  2 = 2.
CHANCE: 5%
: place_hive_portal(_G)
MAP
 1.....2.......1.1
 ..1.....1...2..1.
 ....2.........1..
 1.2....1....2.....2
 .....2.....1....21
 1...1...1.........
  ...2.....1.21...2
  .....1.O......21.
  .1.......1.......
  .2.....1....1..2.
  ...11.2....2...11
  .1..1....1......
    2.......2....1
    1...1.....1
ENDMAP

##############################################################################
# Object lesson
#
NAME:    erik_new_hive_3
TAGS:    uniq_new_hive patrolling no_monster_gen
MONS:    killer bee larva, killer bee, queen bee
MONS:    fire drake / w:5 nothing
SHUFFLE: UVTXYZ, lL4/lL4/lL4/wW5
SUBST:   l=.l, L=l, w=.w, W=w, U==, V=a, T=a, X=a, Y=a, Z=a, '=.a
KMONS:   l = fire drake / w:200 nothing
KFEAT:   l = l
ORIENT:  float
CHANCE: 5%
: place_hive_portal(_G)
MAP
 .............
.'.aa'aa.'.'....
..aaRa'...a'.'a..
.'a2'Y.aaaX.aa.'..
.aV.1aaa'1aaa.'.a.
..aa21'2O2aaa4.aa.
.'.Zaa2131Waaaa...
.'aaaaaRaUa..aa'..
..aa.4aaa.a.'.a...
.aa...aaa.aa......
.a..aaaa.'........
..''..a..a.llllll
 ...'..aaa.lLLLLl
    ...''..llllll
       ........
ENDMAP

#############################################################################
# Another hive entry (Eino)
# This one appeared to me in a dream.
#
NAME:      eino_new_hive_1
TAGS:      uniq_new_hive patrolling no_monster_gen
ORIENT:    float
MONS:      killer bee / killer bee larva w:2
MONS:      queen bee / killer bee w:2
MONS:      oklob plant / plant w:1
MONS:      plant / nothing
KITEM:     R = w:2 honeycomb / w:1 royal jelly
KMONS:     R = killer bee / killer bee larva w:2
KFEAT:     R = .
KMONS:     M = plant
KMONS:     N = plant
KFEAT:     M = *
KFEAT:     N = | / * w:20
KMONS:     Z = plant / nothing
: if crawl.coinflip() then
SUBST:     W : .
SUBST:     w : .
KFEAT:     Z = W
: else
KFEAT:     Z = .
: end
SUBST:     q=w
CHANCE: 5%
: place_hive_portal(_G)
MAP
   x   x
 xxx...xxx
xx4.a1a.4xx
x4.aRaRa.4x
x441aOa244x
x4.aRaRa.4x
xx4.a1a.4xx
 x4.....4x
 xxZ4.44xx
  xZWW.4x
  xxZWZxx
   xxWxx
    xWx
xxxxxWxxxxx
@....W....@
xxxxxWxxxxx
    xWx
   xxWxx
  xx4W4xx
  x4.WW4x
 xx4.WWZxx
 xM4WWww4x
xxN3wwwwwxx
xM4wwwwwqqx
x4.wwwwqqqx
x4..wwqqqqx
xx...44qqxx
 xxx..4xxx
   x   x
ENDMAP

##########################################
# Bees in trees by the pond
NAME:      eino_new_hive_2
TAGS:      uniq_new_hive patrolling no_monster_gen
ORIENT:    float
KFEAT:     N = w / w:5 .
KFEAT:     Z = w
KMONS:     Z = plant / w:3 nothing
KFEAT:     V = w
KMONS:     V = killer bee w:20 / nothing w:6 / vampire mosquito w:1
MONS:      plant / nothing w:3
MONS:      killer bee w:20 / nothing w:6 / vampire mosquito w:1
CHANCE: 5%
: place_hive_portal(_G)
MAP
     ....  ...NNNZNN.
  ....11.....NZZwwZZNN.
 . 11.2111.1NZwwVwwwwwN
.1.1.11.22ZZw.wwwwVwwwZN
..1212211ZZOZwwwwwwwVZZ.
.1..111.21wV1.ZwVwwwZNN
 ..1.2211.1Zw1.ZwwZNN.
  ...11.1...  11.NN..
     ....
ENDMAP



########################################################
# Combs of tragedy (mumra)
# Hive portal map
NAME:     new_hive_mumra_combs
TAGS:     new_hive
ORIENT:	  encompass
# Guarantee a certain amount of combs and larva to demonstrate
# NSUBST:   % : %:5 / . # FIXME: syntax error
# NSUBST:   ! : !:4 / .
KITEM: % = honeycomb
KMONS: ! = killer bee larva
# Perform rest of item/mons substitutions
KITEM: $ = honeycomb w:5 / nothing w:10
KITEM: * = royal jelly / honeycomb w:5 / nothing w:3
KMONS: 1 = killer bee larva w:3 / nothing
KMONS: 2 = killer bee / killer bee larva w:5 / nothing w:2
KMONS: 3 = queen bee
# Either b or c
SHUFFLE: bc/de
SUBST: c=a, e=a
NSUBST: b = 1:. / *:aa.
# Either d or e
NSUBST: d = 1:. / *:aa.
# At least 1 of f
NSUBST: f = 1:. / *:aa.
# At least 1 of g
NSUBST: g = 1:. / *:aa.
# At least 1 of h
NSUBST: h = 1:. / *:aa.
# At least 1 of i
NSUBST: i = 1:. / *:aa.
# At least 1 of j
NSUBST: j = 1:. / *:aa.
# At least 1 of k
NSUBST: k = 1:. / *:aa.
# At least 1 of l
NSUBST: l = 1:. / *:aa.
# At least 1 of m
NSUBST: m = 1:. / *:aa.
# At least 1 of n
NSUBST: n = 1:. / *:aa.
# At least 1 of o
NSUBST: o = 1:. / *:aa.
# At least 1 of p
NSUBST: p = 1:. / *:aa.
# At least 1 of q
NSUBST: q = 1:. / *:aa.
: hive_epilogue(_G)
MAP
             aaaaa     aaaaa
            aa.<>aa   aa...aa
           aa.....aaaaa..$..aa
          aa!%%%%%!aaa..$*$..aa
        aaaaa!%%%!aaaaa..$..aaaaa
       aa...bb!!!cc...dd...ee...aa
      aa..$..aaaaa..$..aaaaa..$..aa
     aa..$*$..aaa..$*$..aaa..$*$..aa
   aaaaa..$..aaaaa..$..aaaaa..$..aaaaa
  aa...ff.1.ff.1.gg.1.gg.1.hh.1.hh...aa
 aa.$$$.aaaaa.$$$.aaaaa.$$$.aaaaa.$$$.aa
aa.$***$.aaa.$***$.aaa.$***$.aaa.$***$.aa
 aa.$$$.aaaaa.$$$.aaaaa.$$$.aaaaa.$$$.aa
  aa1.1ii1.1jj...jj1.1kk1.1kk1.1ll1.1aa
   aaaaa.$$$.aaaaa.$$$.aaaaa.$$$.aaaaa
    aaa1$***$1aaa1$***$1aaa1$***$1aa
      aa.$$$.aaaaa.$$$.aaaaa.$$$.aa
       aa1.1mm...nn1.1nn...oo1.1aa
        aaaaa.....aaaaa.....aaaaa
         aaa111$111aaa111$111aa
           aa11111aaaaa11111aa
            aa111pp$$$qq111aa
             aaaaa11111aaaaa
               aa1122211aa
                aa22322aa
                 aa***aa
                  aaaaa
ENDMAP


NAME:   new_beehive
TAGS:   new_hive
ORIENT: encompass
MONS:   queen bee, killer bee, killer bee larva, plant, yellow wasp
MONS:   random, random
KITEM:  R = w:2 honeycomb / w:1 royal jelly
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxaaaaaaaaaaaRaaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxaaaaaaaaaaRa2aaR1RaaRa2aaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxaaaaaaaaaaRa2a3R3aRaRaRaaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxaaaaRaRaRaaa3aaa3aRa.a.aaaaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxaaaaaaRa.aRa2a2a2a2aRaRa.a.a3aaaaaaaaaaaaaaxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx.4aaaaaaaaa.aaRaRaa2aa2aaRaaa.aa3a33aaaaaaaaaa.44xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx.4aaaaaaa.222a3a.aaaRaaa.aaa.R3aa3a3aaaaaaaa.....4xxxxxxxxxxxxxxxxxxx
xxxxxxxxxx....aaaaaaa.aRa.a3aRaRa.a3a.a.a.a.aRa2aaaaaa....xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx...aaaaaa3a3a.a.a.a3aRa2aRa3a.a.aRaRa.aaaaa...xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx...aa2aRa3a3a3aRa.a3a.a.a.a.a.a.a.a3a.aaa...xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx...aaa.a.a.a2a.aaa.aRaRa2a.a2a3a.a2aaaa..T..xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx.....a2a.a2a.aRaaaaa3a.a.aaa3a3a3a3a.a.........xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx.4...aaRRaa.a2a.a3a3a3a.aaa.a.aRa.a.aa..4.......xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx......a.a.aaa.a3a.a.a.a.aaa2a.a2a.a.aRaa.....4...xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx.....aa3a2aaa.a.a.a3a3a3a3aRaaa.a2a.a2aa........xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx...aaaa.a2aRa.a.a2aaa.a.a.a.aaa.a.aaaa.....xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx..aaa.a.a.a.a.a.a.aaa2a.a3a2a.a2aaa.....xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxx.aaaa3a.a2aRa.a.aaaRa.a.aa.a.aaa....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx...aaaaRa.a3a3a.a.a.aaa.aa.aa....4xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx........aa.a2a.a.aaa2aa.aa.aaa....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx....4.....a.a2a2a.a2a.a2a.......4.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx.............a.a.a.a.a.a.....4....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx..............4..a.a.a......4...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx.................a.a.........xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx........................xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx.....4...T............xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx.......................xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx.........................xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx.................T.........xxxxxxxx..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx.......4.....................xxxxxxx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx..............xx...............xxxxxx....xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx............xxxxx........4......xxxx..4....xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx..T..........xxx................xxxxx...T.xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx............xxx........T.........xxx........xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx....4........xx....................x..........xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx...............x.x...xxx...............xx.xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx.........4...........xxx..................xxxxxxxxxxxxxxxxxxaaaaaxxxxx
xxxxxxxxx.....4.....................4......4...........4...xxxxxxxxxxaa5a5aaxxxx
xxxxxxxxx.................................................wwwwwwwwxxxa5*|*5axxxx
xxxxxxxxx............x...x...T.....xxxx.................wwwwwwwwwwwwxaa*|*aaxxxx
xxxxxxxxxx.........xx.............xxxxx................wwwwwwwwwwwwwwxaa5aaxxxxx
xxxxxxxxxxx.......x..................xxx....4..........wwwwwwwwwwwwwwwxa5axxxxxx
xxxxxxxxxxx.....xxx...4...........................xxxx.4wwwwwwwwwwwwwwwa=axxxxxx
xxxxxxxxxxxx..xxx.............xx....(.........xxxxxxxx....wwwwwwwwwwwwwwaaxxxxxx
xxxxxxxxxxxxxxxx.............xxxx..................xxxx......wwwwwwwwwwxxxxxxxxx
xxxxxxxxxxxxxxxxx....{..<..xxxxxx..<......xxx...........4.wwwwwwwwwwwwxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx........xxx........xxxxxx....4....wwwwwwwwwwwwwwxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx..[.xxx........xxx<....wwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxx.........xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################
# Alternate hive bottoms by R1.
#
# A wand of fire is available at the bottom to melt wax, either to enter the
# hive by the back (much easier fight) or in the middle to reveal the items
# stuck in wax
NAME:   new_beehive2
TAGS:   new_hive
ORIENT: encompass
MONS:   queen bee, killer bee, killer bee larva, plant
MONS:   random, random
ITEM:   wand of fire
NSUBST: M = 10:N / *:R
SUBST:  N = *:30 |
SUBST:  X = xx.
KITEM:  R = w:2 honeycomb / w:1 royal jelly
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxXXXXXXXXXXXXXXXXXXXxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxXXXXX...................Xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxX........................XXxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxXXXXXxXX.......{.......(........XXXXxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxX...............<.......<............XXXXxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxXXXXXXXX.....................P...................XXxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX...............4.....4.............................Xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxXXXXX........................P.4......4....4.......Xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx.a................................................Xxxxxxxxxxxxxxxxx
xxxxxxxxxxxXXa.a............4............................a.a..a.axxxxxxxxxxxxxxx
xxxxxxxxxxX.a.a..a.a.a..................................a.a.aa.aXxxxxxxxxxxxxxxx
xxxxxxxxX.......a.aaa.a......a.a.a.........a.a.a.......a.aaaaRa.Xxxxxxxxxxxxxxxx
xxxxxxxxXX....a..a2aaa....a.a.aa..a.......a.a.a.a.....a.a3aMaa.aXxxxxxxxxxxxxxxx
xxxxxxxx=X...a.a.aaaa.a..a.a.aaaaa.......a.aaaaa.a3a.a.aXx=xxXXXxxxxxxxxxxxxxxxx
xxxxxxxMa.a.a.aaaaa2.a....a.a2aaa.a.a...a.aRaaaRa.a.aXXXxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxaaa.a.aaMMa3a.......a3aRa.a.a.aaaaa3a.a.a.a.a...Xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxa.a.a.aaMaa....4..a.aRa2a.a...aMaaa.a3a.a......XXXXxxxxxxxxxxxxxxxxxxxxx
xxxxxxxa.a...a.aaaRaa......aaaaaa....aaMMa.a.aaaa........a.XXXxxxxxxxxxxxxxxxxxx
xxxxxxxxxX........aa.a....a.aMMMaa....aaaaa3aRaaaa........a.a.Xxxxxxxxxxxxxxxxxx
xxxxxxxXa.a.......a.aRa.a..aaaaaa....a.a.a.a.a.aa.a.a.a..a.aaaXxxxxxxxxxxxxxxxxx
xxxxxxxa.a.........a2aaa.a..a.a.a.....a.aaa.a2a..a.aaa....aXXxxxxxxxxxxxxxxxxxxx
xxxxxxxxXXa...4.....a.aaa............a.aaa.aa.a.aRa.aa.....a.Xxxxxxxxxxxxxxxxxxx
xxxxxxxxxX.........a.aaa.a..a.........aaaaa.aa.a3a.aaa....a.a.Xxxxxxxxxxxxxxxxxx
xxxxxxxxxX..........a.a.a.a...a.a......a.a....aaaaa3a.a....aXXxxxxxxxxxxxxxxxxxx
xxxxxxxxxxXXX........a.a2a.a.a.a.a...........a3aMaaa......XXxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxX.....4.......a.aaa.a.............aaaMa.a.....a.Xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxXX..........a.aaaaa...............aaMaa.....a.aXxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxX...........a.aa................a.aaa.a.a.a.a.Xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxX.....P...........aaa..aaaaaa....a.a.a...XXXXXxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxX...4...........aaaaaa3..aaaaaaa..........Xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxX...............aaa.a.a.a2a.a.a2aaa..........Xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxX............aaa.a.aRa.a.a.a.a.aaaaa.........XXxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxX...........aaaaRa.a2a.a3a.a.a2a.a.aaaa.........XXxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX.........aaaaaa.a3a.a.a.aRa.aRa.aRa.aaaa...4......Xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX.......aaaaa3a.a.a.a3a2a.a.a3a.a.aRa.aaaa.........Xxxxxxxxxxxxxxxxxxx
xxxxxxxxX.........aaaa.a.a3aRa.a2a.a2a2a3a.a.a2a3aaaa........Xxxxxxxxxxxxxxxxxxx
xxxxxxxxX.........aaa.a2aRa.a2a.a.a.a3aRa.a.aRa.a.aaa........Xxxxxxxxxxxxxxxxxxx
xxxxxxxxxX........aa.a.a.a.a.aRa.a3a.a.a2a3a.a.a.aaaa......XXxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX...4..aaaa.a.a3a.a.a.a.aRa3aRa2aRa.a2a.aaa......Xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxX......aa.a2a.a.aRa2a.a.a3a.a.a.a.aRa.3.aa......Xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxX....aa...2a.aRa.a2a.a.a.aRa.a2a3a.a.a..aa..4....Xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxX...aaaaa.a.a2a.a.a2a.a.a.a.a.a.a.aRaa.........Xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxX......aaa.a.a.a.a3a2aRa3a.a3a.a.a3aaaa.......Xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxX.......aaa.aRaRa.a.a.a2a.a.aRa2aRaaaa........Xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxX........aaaa.a.a3a.aRa.a2aRa.a.a.a.aa..........Xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxX...4...aaa.a.a.a.a.a2a.a.a.a.a3a.aa............Xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxX........aaa.aRaRa.a2a.a.a.a.aRa.aa......4....XXXxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxXXX......aaa.a2aRaRaRa.aRa.a.aaaa............Xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxXXXX....aaaaaR3RRRRRR232aaaa..........XXXXxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxXXX..aaaaR3R2R13R.3aaaaaXXXXXXXXXXxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxXXaaaaaR23R2R3Raaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaaaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx.4...<.[..4..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx......d......xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Engaging in the tunnels may be slightly dangerous as they are dead-ends and
# are large enough to allow bees to surround you, but there might be some nice
# items there.
###
NAME:   new_beehive_tunnels
TAGS:   new_hive
ORIENT: encompass
MONS:   queen bee, killer bee larva, killer bee, plant
MONS:   random, random
NSUBST: M = 1:. / *:a
NSUBST: N = 10:% / 6:* / 2:| / *:.
SUBST:  X = xx.
KITEM:  R = w:2 honeycomb / w:1 royal jelly
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxXXXXxxXXxxxxxxxxxxxxxxxxxxxxxxxxxxXXxxXXxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX.........XxxxxxxxxxxxxxxxxxxxxxxxX......Xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX..{..<.4..XxxxxxxxxxxxxxxxxxxxxX...NNN...Xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxX.........XxxxxxxxxxxxxxxxxxxxxX...4.334...Xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxWwwWxxXXXxxxxxxxxxxxxxxxxxxxxxxX.........Xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxWwwWxxxxxxxxxxxxxxxxxxxxxxxxxxxX.3...Xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxWwwWWWWxxxxxxxxxxxxxxxxxxxxxxxX...Xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxWwwwwWWxxxxxxxxxxxxxxxxxxxx.....Xxxxxxxxxxxxxxxxxwwwwwwwwxxxxxxxx
xxxxxxxxxxxxxxxxxxWWwwWWWxxxxxxxxxxxxxxxx...XxxxxxxxxxxxxxxxxxwwwWWWwwWWwwxxxxxx
xxxxxxxxxxxxxxxxxxxxxWwwwWWWxxxxxxxxxxxx..3XxxxxxxxxxxxxxxxxxwwW......3.Wwwxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxWWwWWWxxxxxxxx...XxxxxxxxxxxxxxxxxxxwwW.N4NNN4N.Wwwxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxWwwwWxxxxxxX.3.XxxxxxxxxxxxxxxxxxxxwwW..33...WWwxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxWwwWxxxxxx....xxxxxxxxxxxxxxxxxxxxxwwWW..33Wwwwwxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxWwwWxxxxxxxxX....xxxxxxxxxxxxxxxxxxxxwwwwWWWwWwxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxWWwwWxxxxxxxxxxxX....XxxxxxxxxxxxxxxxxxxxxwWWWwWxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxWwwwWxxxxxxxxxxxxxxX....XxxxxxxxxxxxxxxxxxxxWwwWxxxxxxxx
xxxXX.4WWWwxxxxxxxxxxxWWwwWWxxxxxxxxxxxxxxxxxX3..XxxxxxxxxxxxxxxxxxWwwWxxxxxxxxx
xxx..334WWwwxxxxxxxxWWwwwWxxxxxxxxxxxxxxxxxxxx....xxxxxxxxxxxxxxxxWwwWxxxxxxxxxx
xxX.NNN.4WwwwxxxxxxxxWwwwWxxxxxxxxxxxxxxxxxxxxX.3.XxxxxxxxxxxxxxxWwwWxxxxxxxxxxx
xxxXX..34WWwxxxxxxxxxxWWwwWxxxxxxxxxxxxxxxxxxxx....xxxxxxxxxxxxxWwwWxxxxxxxxxxxx
xxxxxxX3.WWwxxxxxxxxxxxxxWwWWWxxxxxxxxxxxxxxxxx...xxxxxxxxxxxxxWwwWxxxxxxxxxxxxx
xxxxxxx.WWwxxxxxxxxxxxxxxxWWWwWWWxxxxxxxxxxxxxX...xxxxxxxxxxxxxWwwWxxxxxxxxxxxxx
xxxxx.W.wwxxxxxxxxxxxxxxxxxxx..WWWWxxxxxxxxxx....XxxxxxxxxxxxxWwwWxxxxxxxxxxxxxx
xxxx.WWwxxxxxxxxxxxxxxxxxxxxxxW..W.WW..xXXX.......XxxxxxxxxxWWwwwWxxxxxxxxxxxxxx
xxxx.WwxxxxxxxxxxxxxxxxxxxxxxxxxW...WW.aaa..........XXXXXxxWww.wWWXxxxxxxxxxxxxx
xxxx3WwxxxxxxxxxxxxxxxxxxxxXXXxa......aaaaaaa............WWW.3.XxXxxxxxxxxxxxxxx
xxxx..WwwxxxxxxxxxxxxxxxXX...aaaaa....aaaaaaaaaaa.......WW....Xxxxxxxxxxxxxxxxxx
xxxxx.3WWwxxxxxxxxxxxxX.......aaaaaa....a..aaaaaaa...........Xxxxxxxxxxxxxxxxxxx
xxxxxxX..WwwxXXXXXXXx..........aaaa.aa.a.aa.aa.Raaaa.........Xxxxxxxxxxxxxxxxxxx
xxxxxxxxX.....................aaa..a.Ra.a.3a.a.a.aaa.....4...Xxxxxxxxxxxxxxxxxxx
xxxxxxxxxX.............4........a.aaa..a.aa.a2a.a.a...a.......XxxxxxxxXXXXxxxxxx
xxxxxxxxxxxX...............aaa.a.a.a2aa.a3.a.aRa.a.aaaaa.......XxxxxxX.....Xxxxx
xxxxxxxxxxxxww............aaaa.a.a.aa.RaaMaa.2a.a.a..aaaaa.......xxxX...4...xxxx
xxxxxxxxxxwwwwxxX....4....aaa.aRa.a..aaa23Raaa.Ra.a.a..aaa......XxxX...NNN..Xxxx
xxxxxxxxxwwwxxxxx........aaa.a.a3a.a.aR2R2R23aaa.a.a.aaaaaa.....XxxxX...33..xxxx
xxxxxxxxwwwxxxxxxX.......aaa.a.a2aaaaaRR2R23RRa.a3aRa.a3aaa.....xxxxxxX....Xxxxx
xxxxxx.4444xxxxxxxx......aaaa.a3aRa.MR32R1R2RRMa.a.aaa.aaaaaaaXXxxxxxxX....xxxxx
xxxxxx..3.33xxxxxxX.....aaaa.a.a.a.aaaRR2R2R3Ra.a2a.a.a.aaaaaaaxxxxxxX....Xxxxxx
xxxxx.NNNN.xxxxxxxX...aaaaaa2a3a.aa.aaR23232Raa.a.a.aaRaaaaaaaxxxxxX.....Xxxxxxx
xxxxxxNNNNxxxxxxxxxxxaaaa.aa.aRa3a.a.aaaR3Raaa.a.aRa.3a.aaa...XXxXx...XXxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxa...aaa.a.a.a.aR.aaMaa.Raaa3a.aa.aaaa.........XXxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxX....aaaa.a2a.a.aa.a.a.aa...a.32.aaaaa........Xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxX.........aaa.a.a.a.aRaa.a2a.3aaa.a.aa........XXxXxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx.3..XxX....aa.a.a2a.aa.a.a.a.a.3.a.aR.a.....Xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxX..WW..XxxxxX...aaaaa.a.a..a.a3aRa.aaa.a.aaaaa.3.Xxxxxxxxxxxxxxxxxxxx
xxxxxN3WWWWWWW3.XxxxxxX.....aaa.a.a.aa.a.a.a.a...aaaaaaa.....Xxxxxxxxxxxxxxxxxxx
xxxxN43wwWWW.xxxxxxxxxxX.4........aaaaaaaa....aaaaaa.......wwwxxxxxxxxxxxxxxxxxx
xxxx.NwwwWxxxxxxxxxxxxX............aaaaaaa...aaaaaaaa....3.Wwwwwxxxxxxxxxxxxxxxx
xxxxxxWWWWxxxxxxxxxxxxxxxxX........aaa.............aaa....wwWWWwwxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxW.....WWWXXxxxWWWW...4...WWxxxaxXXxxwwwWwwxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxW...WWxxxxxxxxxxxxxWWW...WWWxxxxxxxxxxxxxwWwwxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxW3.WWxxxxxxxxxxxxxxxxxxWWWWWWxxxxxxxxxxxxxxxwWWwxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxW..WxxxxxxxxxxxxxxxxxxxxxxWWWWWxxxxxxxxxxxxxxxwwWwxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxW..WxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxwwWwxxxxxxxxxx
xxxxxxxxxxxxxxxxxWW.WxxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWxxxxxxxxxxxxxxxwwWwxxxxxxxxx
xxxxxxxxxxxxxxxxWW.3WxxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxxwWwwxxxxxxx
xxxxxxxxxxxxxxWW..4WWxxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxxxWwwwxxxxxx
xxxxxxxxxxxxxW..334..WxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWxxxxxxxxxxxxxxxxxxxWwwxxxxxx
xxxxxxxxxxxxxxW.NNN.WxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWxxxxxxxxxxxxxxxxxxxwWwwxxxxxx
xxxxxxxxxxxxxxx..W.WxxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxxxX..4Wwxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxxxX..(...wwxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxxxxxxX..<.Xxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxWWWWxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.WWWWWxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.4[..4WWxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx....<..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Corners have three hives and one stash of items guarded by oklobs; river can
# be crossed by anyone with a wand of fire.
#
NAME:   new_minmay_beehive_corners
TAGS:   new_hive
ORIENT: encompass
MONS:   queen bee, killer bee larva, killer bee, plant, oklob plant
KITEM:  R = w:2 honeycomb / w:1 royal jelly
SUBST:  w = w:20 W:1
SUBST:  Y = wW
# Add one escape hatch, so that players using teleportation won't strand in
# the Southern part. The hatch can be inconveniently placed, however.
NSUBST: < = 1:< / *:x
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxaaaaaaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxaaRa3a2aaaxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaaaaaxxxxxxx
xxxxxxxxxaaa2aR.aaRaaaaaaxxxxxxxxxxxxxxxxxx{[(xxxxxxxxxxxxaaaaaa3aRa2Ra3aaaaxxxx
xxxxxxxaaaaRaRaa3.a.a2a3aaxaaxxxxxxxxxxx4........xxxxxxaaaaRa2a.aRa.aaRa.a2aaxxx
xxxxxxaa3aRaa2a.aRaaRa.a.aaa.4xxxxxxxxxx.........4xxxxx.a2Ra3a.a2aaRa3a.aRa.axxx
xxxxxxa2aaa3aa3aaa..a3a.a.........xxxxx4............4..3..a.a.aRa3.a.a2a.a3aaxxx
xxxxxxaaRa2a..a.aRa3aa.a3.................................3..a.aRa2aaRaRa.aRaaxx
xxxxxxxaa3a.a2Ra3a.a.........................................3a.a.aR.a3a.aRaaxxx
xxxxxxxxa2aa.aa.a.a.3..............................xx...........3a.a3.a2a3aaxxxx
xxxxxxxxaaR2aa.a3...........4......................4xx............a.aa.aaaaxxxxx
xxxxxxxxxaaa.3a.............xx....................................3a2.aaxxxxxxxx
xxxxxxxxxxxaaa.3...........xxxx.............a3..a....................3axxxxxxxxx
xxxxxxxxxxxxxaa...........xxxxxx4..........aRaa2...................3.aaxxxxxxxxx
xxxxxxxxxxxxxxxx..........xxxx............3.2aRa3.........4.........xxxxxxxxxxxx
xxxxxxxxxxxxxxxx........4xxx................3...........4x.........xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx.........xx.......4..........2a...................xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx.................................................axxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx4...............................................aaxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx..................................4.......4x.aaaaaxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwww.........4......................wwwwwwwxxaa..aaxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwwwwww....wwww..................wwwwwwwwwwxxxaa.axxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwww......4wwwwwwwwwwwwwwwwwwxxa.aaxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxaaWYaaxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwYWYwwwxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwYWYwwwxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwYWYwxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.....wxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.4..wwww.........4xxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwww4...........................xxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxwww..4..wwwwwww...................a..3a...........xxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx...................................2a.3a..........xxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx.............a...................a3aRaa2.a.........xxxxxxxxxx
xxxxxxxxxxxxxxxxxx................3a..........4....3.aRaR.a..........axxxxxxxxxx
xxxxxxxxxxxxxxxxx4.............a3aa2.a.............a..2.a3.........a3aaxxxxxxxxx
xxxxxxxxxxxxxxxxx...............aR.a.................3a........3....aRaaaaxxxxxx
xxxxxxxxxxxxxxxx...............a...............................a.3a3.a.a3aaxxxxx
xxxxxxxxxxxxxx4..................xxxx4...........4........a..a3.aR.a2a3Ra2aaaxxx
xxxxxxxxxxx................xxxxxxxxxxxx.....4xxxxxx....aaa3a2R.a2.aRa.aa.a.3aaxx
xxxxxxxxx<4..5........xxxxxxxxxxxxxxxxxxxxxxx<xxxxxxxxxxxaaaa3a3aa.a..a3.Raa2axx
xxxxxxxx4...***....4xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaa3a..a3aaR.a3a..aax
xxxxxxxx...5|||5...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaa2Ra2aR2aa.a3aaRax
xxxxxxxx4...***...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxa3aa3a.aR1Ra.a.Raax
xxxxxxxxx4...5.4xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaR3a.aR2R2Ra3.aaax
xxxxxxxxxxxx4.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaRa2aRaRaRaa3axx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaa3a3a2a3aR2aaxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaa3a3a2aaaaaxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxaaaaaaaxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    new_beehive_diamond
ORIENT:  encompass
TAGS:    new_hive no_rotate
SHUFFLE: {([
SUBST:   A = a:3 .:2, B = a:3 x:2, ' = . 1:3 d:1
KMONS:   1 = killer bee
KMONS:   2 = queen bee
KMONS:   3 = oklob plant
ITEM:    honeycomb / royal jelly w:4
KITEM:   1 = nothing / honeycomb w:5 / royal jelly w:2
KITEM:   2 = nothing / royal jelly
: spotty_map { boxy = false, iterations = 400 + crawl.random2(300) }
MAP
                   aaaaa
                  aa*|*aa
                  a3*|*3a
                  a3*|*3a
                  aaa=aaa
                aaa11211aaa
               aa'''111'''aa
             aaa''a''A''a''aaa
            aa'''aaa'''aaa'''aa
          aaa''a''a''a''a''a''aaa
         aa'''aaa'''aaa'''aaa'''aa
       aaa''a''a''a''a''a''a''a''aaa
       a'''aaa'''aaa'''aaa'''aaa'''a
      xx'a''a''a''a''a''a''a''a''a'xx
    xxxxaaa'''aaa'''aaa'''aaa'''aaaxxxx
   xxxBx'a''a''a''a''a''a''a''a''a'xBxxx
   xxBBB'''aaa'''aaa'''aaa'''aaa'''BBBxx
   xxxBx'a''a''a''a''a''a''a''a''a'xBxxx
   xxxxxaaa'''aaa'''aaa'''aaa'''aaaxxxxx
   xxxBx'a''a''a''a''a''a''a''a''a'xBxxx
   xxBBB'''aaa'''aaa'''aaa'''aaa'''BBBxx
   xxxBx'A''a''a''a''a''a''a''a''A'xBxxx
   xxxxxAAA'''aaa'''aaa'''aaa'''AAAxxxxx
   xxxxx.A''A''a''a''a''a''a''A''A.xxxxx
   xxxxx...AAA'''aaa'''aaa'''AAA...xxxxx
 xxxxxxxxx..A''A''a''a''a''A''A..xxxxxxxxx
xxxxxxxxxxx...AAA...aaa...AAA...xxxxxxxxxxx
xxxxxxxxxxxxx..A..A..a..A..A..xxxxxxxxxxxxx
xxxxxxxxxxxxxx...............xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx{xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx.x.xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx.xxx.xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx.xxxxx.xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx.xxxxxxx.xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx.xxxxxxxxx.xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx.xxxxxxxxxxx.xxxxxxxxxxxxxxx
xxxxxxxxxxxxxx(xxxxxxxxxxxxx[xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP
