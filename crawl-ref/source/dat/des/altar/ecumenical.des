###############################################################################
# ecumenical.des: This file contains the vaults that place faded altars and
#                 are a part of the D:1-3 chance group for placing such an
#                 altar. Vaults that use ecumenical altars for decorative
#                 reasons should not be in this file.
#
# See the section in docs/develop/levels/guidelines.md for guidelines on
# creating new vaults. Every vault in this file should call
# ecumenical_altar_setup to set the chance.
#
###############################################################################

{{
function ecumenical_altar_setup(e)
  e.depth("D:1-3")
  e.tags("uniq_ecumenical_altar chance_ecumenical_altar extra")
  e.chance(5000)
  e.kfeat("_ = altar_ecumenical")
end
}}

NAME:   basic_ecumenical_altar
TAGS:   transparent
WEIGHT: 200
: ecumenical_altar_setup(_G)
MAP
_
ENDMAP

NAME:    chequers_ecumenical_altar_simple
TAGS:    transparent
SHUFFLE: GH
SUBST: G : Gbt x:5
: ecumenical_altar_setup(_G)
MAP
.....
.HGH.
.G_G.
.HGH.
.....
ENDMAP

NAME:    chequers_ecumenical_altar_simple_redux
TAGS:    transparent
SHUFFLE: GH
SUBST:   G : Gbt x:5
: ecumenical_altar_setup(_G)
MAP
.......
..HGH..
.HGHGH.
.GH_HG.
.HGHGH.
..HGH..
.......
ENDMAP

NAME:   chequers_ecumenical_altar_grove
TAGS:   transparent
WEIGHT: 5
KMASK:  - = no_item_gen
KMASK:  - = no_monster_gen
KPROP:  - = no_tele_into
SUBST:  - = tt.
: if crawl.one_chance_in(4) then
: set_feature_name("tree", "dead tree")
TILE:    t = dngn_tree_dead
COLOUR: t = w:5 lightgrey / w:5 darkgrey / brown
: elseif crawl.one_chance_in(4) then
COLOUR: t = lightred / red w:4 / yellow w:4
TILE:   t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
: end
: ecumenical_altar_setup(_G)
MAP
 ......
 .-.-.@..
--.-..-.--.
 .tttttt.--.
.-t-...t.---.
.tt.-tt.-t--.
-t--.-t--t-.
 ttt-.ttttt-.
 t.t.-.-t-t.
 -t._tttt-@.
 ttttt--tt.
-ttt--.---.
...........
ENDMAP

NAME: chequers_ecumenical_altar_lobes
WEIGHT: 5
KPROP:  tG = no_tele_into
KMASK:  tGp = no_item_gen
KMASK:  tG = no_monster_gen
NSUBST: + = 1:.
SUBST:  ' : tGp, t = t:2 ., G = G:3 ., p = p:5 ., - = T U V .
KMONS: p = plant
: ecumenical_altar_setup(_G)
MAP
   xxxxx    xxxxx
 xxx'-'xxxxxx'-'xxx
xxtGp_pGtxxtGp.pGtxx
xxtGp.pGtxxtGp.pGtxx
xxtGp.pGtxxtGp.pGtxx
xxxxx+xxxxxxxx+xxxxx
xtGpp..........ppGtx
xxxtGpppp..ppppGtxxx
  xxxxxtG..Gtxxxxx
      xxx..xxx
        x..x
         @@
ENDMAP

NAME: chequers_ecumenical_altar_twisty
TAGS: transparent
WEIGHT: 5
KFEAT:  _ = altar_ecumenical
SUBST: x : x:30 c t:1
: ecumenical_altar_setup(_G)
MAP
 x@x
 x.xx
 xx.x
xx.xx
x.xx
xx_xx
 xx.x
xx.xx
x.xx
xx.x
 x@x
ENDMAP

NAME: chequers_ecumenical_altar_island
SUBST: - = W:5 . t:5
SUBST: s = W:5 .
SUBST: S = w.
: ecumenical_altar_setup(_G)
MAP
sssssssssssss
sSSSSSSSSSSSs
sSwwwwwwwwwSs
sSwwwwwwwwwSs
sSwWWWWwwwwSs
sSWww--WwwwSs
sSwWw-_-wwwSs
sSwWw---wwwSs
@.WwwwwwwwwSs
sSwwwwwwwwwSs
sSwwwwwwwwwSs
sSSSSSSSSSSSs
sssssssssssss
ENDMAP

NAME:   ebering_ecumenical_altar_webs
TAGS:   transparent
NSUBST: ^ = 4=^. / 4=^..
KFEAT:  ^ = web trap
: ecumenical_altar_setup(_G)
MAP
^^^
^_^
^^^
ENDMAP

NAME: ebering_ecumenical_altar_mist
TAGS:   transparent
MARKER: _ = lua:fog_machine { cloud_type = "thin mist", pow_min = 5, \
                              pow_max = 10, delay = 20, size = 1, \
                              spread_rate = 15, walk_dist = 2 }
: ecumenical_altar_setup(_G)
MAP
...
._.
...
ENDMAP

NAME:  ebering_ecumenical_altar_box
TAGS:  transparent
SUBST:  x : xcvb
: ecumenical_altar_setup(_G)
MAP
.......
.xx.xx.
.x...x.
..._...
.x...x.
.xx.xx.
.......
ENDMAP

NAME:  ebering_ecumenical_altar_hall
TAGS:  transparent
SUBST: G : btAGTUV Y:1
: ecumenical_altar_setup(_G)
MAP
xxxxxxx
xGxGxGx
@.._..@
xGxGxGx
xxxxxxx
ENDMAP

NAME:  ebering_ecumenical_altar_sacrificial
TAGS:  transparent
KITEM: _ = dagger mundane
SUBST: ' = ''.
KPROP: ' = bloody
: ecumenical_altar_setup(_G)
MAP
'''''
'''''
''_''
'''''
'''''
ENDMAP

NAME:   ebering_ecumenical_altar_rust
TAGS:   transparent
KITEM:  _ = any weapon damaged
KFEAT:  a = iron_grate
NSUBST: ' = 2=va / 4=va. / .
: ecumenical_altar_setup(_G)
MAP
'''
'_'
'''
ENDMAP

NAME:  ebering_ecumenical_altar_abandoned
TAGS:  transparent
KFEAT: s = abandoned_shop
: ecumenical_altar_setup(_G)
MAP
._.
...
V.S
ENDMAP

NAME:   ebering_ecumenical_altar_ruins
WEIGHT: 5
TAGS:   transparent
SUBST:  G = GGG-
SUBST:  - = --.
COLOUR: - = brown
FTILE:  -G = floor_rough_brown
COLOUR: G = lightgrey
TILE:   G = dngn_crumbled_column
: set_feature_name("granite_statue", "broken pillar")
: ecumenical_altar_setup(_G)
MAP
  G-G
  .-.
G.G-G.G
---_---
G.G-G.G
  .-.
  G-G
ENDMAP

NAME: hellmonk_ecumenical_altar_pennies_from_heaven
: ecumenical_altar_setup(_G)
TAGS:   no_monster_gen
KITEM: _ = gold q:1
KFEAT: W = shallow_water
KITEM: W = gold q:1 / w:25 nothing
MAP
.....
.WWW.
.W_W.
.WWW.
.....
ENDMAP

NAME: hellmonk_ecumenical_altar_leap_of_faith
: ecumenical_altar_setup(_G)
KFEAT: ^ = shaft trap
MAP
xxx
x_x
x^x
x@x
ENDMAP

NAME: hellmonk_ecumenical_altar_run_dry
: ecumenical_altar_setup(_G)
SUBST: V = V.
MAP
V.V.V
.....
V._.V
.....
V.V.V
ENDMAP

NAME: hellmonk_ecumenical_altar_heavens_door
: ecumenical_altar_setup(_G)
MAP
 +++++
+++++++
+++++++
+++_+++
+++++++
+++++++
 +++++
   @
ENDMAP

NAME: hellmonk_ecumenical_altar_blasted
: ecumenical_altar_setup(_G)
KITEM: _ = scroll of immolation ident:type
SUBST: x = .:15 x
SUBST: x : xc
MAP
xxxxxxxxxxx
xxxxxxxxxxx
xx.......xx
xx.......xx
xx.......xx
xx..._...xx
xx.......xx
xx.......xx
xx.......xx
xxxxx.xxxxx
xxxxx+xxxxx
     @
ENDMAP

NAME: hellmonk_ecumenical_altar_lost_god
: ecumenical_altar_setup(_G)
SUBST: x : x:20 c m:1
NSUBST: a = 1:_ / *.
FTILE: . = floor_pebble
MAP
..............
.x......x...x.
.xa....ax.a.x.
.x......x...x.
.a............
.....a........
.x...x..x.....
.x.a.x.ax.....
.x...x..xaxxx.
............a.
ENDMAP

NAME: hellmonk_ecumenical_altar_crossroads
: ecumenical_altar_setup(_G)
KFEAT: r = abandoned_shop / G / l / t / V / T / web / shaft trap / \
            net trap / A / > / Y w:2
KMASK: r = no_monster_gen
MAP
@@@xxxxxxx@@@
x...xxxxx...x
xx...xxx...xx
 xx...r...xx 
  xx.._..xx 
   xx...xx 
    x...x 
    x...x
    x@@@x 
ENDMAP

NAME: hellmonk_ecumenical_altar_zigzags
: ecumenical_altar_setup(_G)
SUBST: a : xcv, d : xcv, e : cvb
MAP
                             ddddddd
                           ddd.....ddd
       aaaaaaa           ddd.........ddd
     aaa.....aaa       ddd.............ddd
   aaa.........aaa.......................@
 aaa.........................
@.........................eee
            eee.........eee
              eee.._..eee
                eeeeeee
ENDMAP

NAME: hellmonk_ecumenical_altar_inner_sanctum
: ecumenical_altar_setup(_G)
NSUBST: d = 1:+ / *:x
NSUBST: e = 1:+ / *:c
NSUBST: f = 1:+ / *:v
SUBST: v : vbtm
MAP
.................
.xxxxxxxdxxxxxxx.
.x.............x.
.x.............x.
.x..ccccecccc..x.
.x..c.......c..x.
.x..c.......c..x.
.x..c..vfv..c..x.
.d..e..f_f..e..d.
.x..c..vfv..c..x.
.x..c.......c..x.
.x..c.......c..x.
.x..ccccecccc..x.
.x.............x.
.x.............x.
.xxxxxxxdxxxxxxx.
.................
ENDMAP

NAME: hellmonk_ecumenical_altar_the_god_not_taken
: ecumenical_altar_setup(_G)
:if crawl.one_chance_in(10) then
COLOUR:  t = lightred / red w:3
:else
COLOUR:  t = yellow / green w:3
:end
MAP
 tttt         tttt 
tt.ttt       ttt.tt
t._.tt       tt._.t
t...tt       tt...t
t...tt       tt...t
t...tt       tt...t
tt...tt     tt...tt
 tt...tt   tt...tt
  tt...tt tt...tt
   tt...ttt...tt
    tt...t...tt
     tt.....tt
      tt...tt
      tt...tt
      tt@@@tt
ENDMAP

NAME: hellmonk_ecumenical_altar_arch_garden
: ecumenical_altar_setup(_G)
NSUBST: A = 1:_ / *:A
SUBST: A = tGAA.
SUBST: . = ..'
FTILE:  ._t = floor_grass
FTILE:  'GA = floor_dirt
MAP
A..A..A..A..A
.............
.............
A..A..A..A..A
.............
.............
A..A..A..A..A
.............
.............
A..A..A..A..A
.............
.............
A..A..A..A..A
ENDMAP

NAME: hellmonk_ecumenical_altar_watching_god
: ecumenical_altar_setup(_G)
KMONS: 1 = floating eye / golden eye / shining eye / eye of devastation / \
            great orb of eyes
MAP
ccc@@@ccc
c1m...m1c
cmm...mmc
@.......@
@..._...@
@.......@
cmm...mmc
c1m...m1c
ccc@@@ccc
ENDMAP

NAME: hellmonk_ecumenical_altar_dog_temple
WEIGHT: 5
: ecumenical_altar_setup(_G)
TAGS:   no_monster_gen
: if you.absdepth() > 2 and crawl.coinflip() then
KMONS: a = hound
: else
KMONS: a = jackal band
: end
KFEAT: a = T
MAP
.......
.G.a.G.
.......
.T._.T.
.......
.G.T.G.
.......
ENDMAP

NAME: hellmonk_ecumenical_altar_prayer_group
WEIGHT: 5
: ecumenical_altar_setup(_G)
SUBST: 1 = 1..
: if you.absdepth() > 2 then
MONS: orc priest w:2 / gnoll shaman w:3 / orc
KITEM: _ = robe / amulet of faith w:1 / manual of invocations w:2
: else
MONS: hobgoblin / goblin / kobold / nothing w:20
: end
MAP
 ccccccc
cccG_Gccc
cc..1..cc
cc.1.1.cc
c.1...1.c
c..G1G..c
c.......c
c.......c
c.......c
cc.G.G.cc
cc.....cc
 cc...cc 
 cc@@@cc
ENDMAP

#an ooze ate the god's name right off the altar
NAME: hellmonk_ecumenical_altar_slurped_away
WEIGHT: 5
: ecumenical_altar_setup(_G)
SUBST: x : xc, g = G.
: if you.absdepth() > 2 and crawl.one_chance_in(3) then
KMONS: _ = jelly
KFEAT: + = runed_clear_door
:else
KMONS: _ = endoplasm
:end
MAP
 @       @
@+xxx xxx+@
 x..xxx..x
 x...g...x
 xx.....xx
  xg._.gx 
 xx.....xx
 x...g...x
 x..xxx..x
@+xxx xxx+@
 @       @
ENDMAP

# some orcs have torn down the altar and replaced it with their own
NAME: hellmonk_ecumenical_altar_defaced_shrine
WEIGHT: 2
: ecumenical_altar_setup(_G)
TAGS:   no_monster_gen
KFEAT: B = altar_beogh
KPROP: B = no_tele_into
SHUFFLE: d_
NSUBST: 2 = 1:2 / *:.
SUBST:  1 = 1..
SUBST:  x : xc
SUBST: . = .:90 '
KPROP: ' = bloody
KITEM: _ = stone q:1 / large rock q:1 w:3
: if you.absdepth() > 1 then
MONS: orc, orc / orc wizard w:1 / orc priest w:1
:else
MONS: rat, giant cockroach / dart slug
:end
{{
  dgn.delayed_decay(_G, 'd', "human skeleton / kobold skeleton / elf skeleton"
                             .. " / gnoll skeleton / ogre skeleton"
                             .. " / naga skeleton / minotaur skeleton"
                             .. " / merfolk skeleton / tengu skeleton"
                             .. " / draconian skeleton")
}}
MAP
       xxx
       x_x
  xxxxxx+xxxxxx
  xxx...2...xxx
  xx.1.....1.xx
  x.....1.....x
xxx....vlv....xxx
xd+2...lBl...2+dx
xxx....vlv....xxx
  x..1.....1..x
  xx....1....xx
  xxx.......xxx
  xxxx.....xxxx
  xxxx.....xxxx
  xxxx@@@@@xxxx
ENDMAP