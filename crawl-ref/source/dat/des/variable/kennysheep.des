# c: outer vault walls
# x: inner vault walls (could be see-through blocks, or not there at all)
# t: doodads. fountains, statues or the more decorative walls. Sometimes get replaced by normal walls
# w: water or lava or trees. or nothing
# D/F opposite/adjacent vault entrances, that may or may not be there
# E/H placed in front of the alternate doors. become w tiles if there's no door there
{{
function ks_random_setup(e, norandomexits)
    e.tags("no_pool_fixup")
    -- 1/2 chance the adjacent door is there, followed by a 1/2 chance every
    -- side has a door.
    if norandomexits == nil then
        if crawl.one_chance_in(2) then
            e.tags("transparent")
            e.subst("D : +")
            e.subst("E : .")
            if crawl.one_chance_in(2) then
                e.subst("F : +")
                e.subst("H : .")
            else
                e.subst("F : c")
                e.subst("H : w")
            end
        else
            e.subst("DF : c")
            e.subst("EH : w")
        end
    end
    --rooms have a 1/2 chance of being bordered by water or trees
    --q is used as a placeholder for trees to keep them from being
    -- re-selected in the next step.
    e.subst("w : w.")
    e.subst("w : wwwWWqt")
    --room setups:
    --0 : doodads replaced with walls
    --1 : walls replaced with water/lava or removed. doodads may or may not be walls
    --2 : doodads picked from more obvious vault decorations
    selected = crawl.random2(3)
    if selected == 0 then
        e.subst("t : x")
        e.subst("x : cvbttm")
    elseif selected == 1 then
        e.subst("x : ..wW")
        e.subst("t : .TmbttwG")
    elseif selected == 2 then
        e.subst("t : .TttG")
        e.subst("x : cvbtt")
    end
    -- Turn q into t for tree borders.
    e.subst("q : t")
    -- The outer walls are probably stone or rock, but can be metal or crystal.
    e.subst("c : ccccxxxxvb")
    -- One chance in three of turning all water on the floor into lava.
    -- Tree and fountain tiles are changed so the vault looks normal.
    if crawl.one_chance_in(3) then
        e.subst("Wwt : l")
        e.subst("T : V")
    end
end
}}

# Purely decorative vaults.

NAME:  kennysheep_path_1
TAGS:  extra decor
DEPTH: D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
cccccccFccccccc
cwwwwwwHwwwwwwc
cw...........wc
cw...........wc
cw..txxxxxt..wc
cw..xxcccxx..wc
cw..xcc ccx..wc
+...xc   cx..ED
cw..xcc ccx..wc
cw..xxcccxx..wc
cw..txxxxxt..wc
cw...........wc
cw...........wc
cwwwwwwHwwwwwwc
cccccccFccccccc
ENDMAP

NAME:  kennysheep_flooded_box
TAGS:  extra decor
DEPTH: D, Depths
SUBST: B : Bt
: ks_random_setup(_G)
MAP
 cccDccc
ccwwEwwcc
cwwwEwwwc
cwwwEwwwc
FHHHBHHHF
cwww.wwwc
cwww.wwwc
ccww.wwcc
 ccc+ccc
ENDMAP

NAME:  kennysheep_pointless_box
TAGS:  extra decor
DEPTH: D, Depths
: ks_random_setup(_G)
MAP
FccccccccccD
cHwwwwwwwwEc
cw........wc
cw.txxxxt.wc
cw.xxxxxx.wc
cw.xxccxx.wc
cw.xxccxx.wc
cw.xxxxxx.wc
cw.txxxxt.wc
cw........wc
c.wwwwwwwwHc
+ccccccccccF
ENDMAP

NAME:  kennysheep_minor_cross
TAGS:  extra decor
DEPTH: D, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
     cccDccc
     cwwEwwc
     cw...wc
     cw...wc
     cw...wc
ccccccwx.xwcccccc
cwwwwwwx.xwwwwwwc
cw...xxt.txx...wc
FH.............HF
cw...xxt.txx...wc
cwwwwwwx.xwwwwwwc
ccccccwx.xwcccccc
     cw...wc
     cw...wc
     cw...wc
     cww.wwc
     ccc+ccc
ENDMAP

NAME:  kennysheep_another_room
TAGS:  extra decor
DEPTH: D, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
 ccccDccc
ccwwwEwwcc
cww....wwc
cw..xt..wc
FH.txxx.wc
cw.xxxt.HF
cw..tx..wc
cww....wwc
ccww.wwwcc
 ccc+cccc
ENDMAP

NAME:  kennysheep_small_spiral
TAGS:  extra decor
DEPTH: D, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
ccccccccccFcc
cwwwwwwwwwHwc
+..........wc
cwxxxxxxxx.wc
cw.......x.wc
cw.xxxxx.x.wc
cw.x..*x.x.wc
cw.x.xtx.x.wc
cw.x.xxx.x.wc
cw.x.....x.wc
cw.xxxxxxx.wc
cw.........ED
cwHwwwwwwwwwc
ccFcccccccccc
ENDMAP

NAME:  kennysheep_nondescript
TAGS:  extra decor
DEPTH: D, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
  ccDc
cFctEc
c...wc
c.wwwc
c.wccc
c+cc
ENDMAP

NAME:  kennysheep_big_empty_room
TAGS:  extra decor
DEPTH: D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
     ccccFFcccc
    ccwwwHHwwwcc
   ccww......wwcc
  ccww........wwcc
 ccww..........wwcc
ccww............wwcc
cww....wwwwww....wwc
cw....wtxxxxtw....wc
cw....wxxxxxxw....wc
+.....wxxxxxxw....ED
+.....wxxxxxxw....ED
cw....wxxxxxxw....wc
cw....wtxxxxtw....wc
cww....wwwwww....wwc
ccww............wwcc
 ccww..........wwcc
  ccww........wwcc
   ccww......wwcc
    ccwwwHHwwwcc
     ccccFFcccc
ENDMAP

NAME:  kennysheep_cross_room
DEPTH: D:2-, Depths, !D:$, !Depths:$
TAGS:  extra decor
: ks_random_setup(_G)
MAP
     ccccFFcccc
    ccwwwHHwwwcc
   ccww......wwcc
  ccww...xx...wwcc
 ccww....xx....wwcc
ccww.....xx.....wwcc
cww......xx......wwc
cw......xxxx......wc
cw.....xtxxtx.....wc
+..xxxxxxxxxxxxxx.ED
+..xxxxxxxxxxxxxx.ED
cw.....xtxxtx.....wc
cw......xxxx......wc
cww......xx......wwc
ccww.....xx.....wwcc
 ccww....xx....wwcc
  ccww...xx...wwcc
   ccww......wwcc
    ccwwwHHwwwcc
     ccccFFcccc
ENDMAP

NAME:  kennysheep_cluttered_room
TAGS:  extra decor
DEPTH: D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
      cccFFFccc
     ccwwHHHwwcc
    ccwwx...xwwcc
   ccwwx.x.x.xwwcc
  ccwwx...x...xwwcc
 ccwwx.x.x.x.x.xwwcc
ccwwx...x.t.x...xwwcc
cwwx.x.x.x.x.x.x.xwwc
cwx...x...x...x...xwc
+..x.x.x.x.x.x.x.x.ED
+...x.t.x.t.x.t.x..ED
+..x.x.x.x.x.x.x.x.ED
cwx...x...x...x...xwc
cwwx.x.x.x.x.x.x.xwwc
ccwwx...x.t.x...xwwcc
 ccwwx.x.x.x.x.xwwcc
  ccwwx...x...xwwcc
   ccwwx.x.x.xwwcc
    ccwwx...xwwcc
     ccwwHHHwwcc
      cccFFFccc
ENDMAP

NAME:  kennysheep_mini
TAGS:  extra decor
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
KFEAT: O = any shop
SUBST: {={}OCB
MAP
cDc
F{F
c+c
ENDMAP

NAME:  kennysheep_mini_moat
TAGS:  extra decor
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
SUBST: . : @
KFEAT: O = any shop
SUBST: { = {}OCB
MAP
wwEww
wcDcw
HF{FH
wc+cw
ww@ww
ENDMAP

NAME:  kennysheep_cross_bottleneck
TAGS:  extra decor
DEPTH: D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
        cFFFc
        cHHHc
        cw.wc
        cw.wc
        cw.wc
        cw.wc
        cw.wc
        cw.wc
cccccccccx.xccccccccc
+.wwwwwwxx.xxwwwwwwED
+.........t........ED
+.wwwwwwxx.xxwwwwwwED
cccccccccx.xccccccccc
        cw.wc
        cw.wc
        cw.wc
        cw.wc
        cw.wc
        cw.wc
        cHHHc
        cFFFc
ENDMAP

NAME:  kennysheep_donut
TAGS:  extra decor
DEPTH: D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
      cccFFFccc
     cct.....tcc
    cc.........cc
   cc...........cc
  cc...wwwxwww...cc
 cc...wwxxxxxww...cc
cc...wwxxcccxxww...cc
ct..wwxxcc ccxxww..tc
c...wxxcc   ccxxw...c
+...wxcc     ccxw...D
+...xxc       cxx...D
+...wxcc     ccxw...D
c...wxxcc   ccxxw...c
ct..wwxxcc ccxxww..tc
cc...wwxxcccxxww...cc
 cc...wwxxxxxww...cc
  cc...wwwxwww...cc
   cc...........cc
    cc.........cc
     cct.....tcc
      cccFFFccc
ENDMAP

NAME:  kennysheep_nub
TAGS:  extra decor
DEPTH: D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G, true)
MAP
  ccccc
 ccxxxcc
ccwwtwwcc
cww...wwc
cw.....wc
c@.....@c
ENDMAP

NAME:  kennysheep_crisscross
TAGS:  extra decor
DEPTH: D, Depths
: ks_random_setup(_G, true)
MAP
 .x.x.x.x.x.x.
.t...t...t...t.
x.x.x.x.x.x.x.x
...t...t...t...
x.x.x.x.x.x.x.x
.t...t...t...t.
x.x.x.x.x.x.x.x
...t...t...t...
x.x.x.x.x.x.x.x
.t...t...t...t.
x.x.x.x.x.x.x.x
...t...t...t...
x.x.x.x.x.x.x.x
.t...t...t...t.
 .x.x.x.x.x.x.
ENDMAP

NAME:  kennysheep_doodads
TAGS:  extra decor
DEPTH: D, Depths
: ks_random_setup(_G, true)
MAP
      .x.
    ..xtx..
   .x..x..x.
 ..xtx...xtx..
.x..x..x..x..x.
xtx...xtx...xtx
.x..x..x..x..x.
...xtx...xtx...
.x..x..x..x..x.
xtx...xtx...xtx
.x..x..x..x..x.
 ..xtx...xtx..
   .x..x..x.
    ..xtx..
      .x.
ENDMAP

NAME:  kennysheep_decorative_pillar
TAGS:  extra decor transparent
DEPTH: D, Depths, !D:$, !Depths:$
: ks_random_setup(_G, true)
MAP
     ...xtx....
    .....x......
   ..............
  ..x..........x..
 ..xx..........xx..
....................
.......wwwwww.......
......wwccccww......
......wccccccw.....x
x.....wccccccw....xt
tx....wccccccw.....x
x.....wccccccw......
......wwccccww......
.......wwwwww.......
....................
 ..xx..........xx..
  ..x..........x..
   ..............
    ......x.....
     ....xtx...
ENDMAP

# Vaults with monsters and loot.

NAME:   kennysheep_big_flooded_room
ORIENT: float
DEPTH:  D:2-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
     ccccFFcccc
    ccwwwHHwwwcc
   ccwwwwHHwwwwcc
  ccwwwwwHHwwwwwcc
 ccwwwwwwHHwwwwwwcc
ccwwwwwwwHHwwwwwwwcc
cwwwwwwwwHHwwwwwwwwc
cwwwwwwwwHHwwwwwwwwc
cwwwwwwwt.9twwwwwwwc
+........xx*EEEEEEED
+........xx*EEEEEEED
cwwwwwwwt.9twwwwwwwc
cwwwwwwwwHHwwwwwwwwc
cwwwwwwwwHHwwwwwwwwc
ccwwwwwwwHHwwwwwwwcc
 ccwwwwwwHHwwwwwwcc
  ccwwwwwHHwwwwwcc
   ccwwwwHHwwwwcc
    ccwwwHHwwwcc
     ccccFFcccc
ENDMAP

NAME:   kennysheep_cluttered_fort
ORIENT: float
DEPTH:  D:15-, Depths, !D:$, !Depths:$
NSUBST: % : 1:|
SUBST:  % = *
SUBST:  9 = 9900.
: ks_random_setup(_G)
MAP
      cccFFFccc
     ccwwHHHwwcc
    ccwwx...xwwcc
   ccwwx.x.x.xwwcc
  ccwwx...x...xwwcc
 ccwwx.x.xtx.x.xwwcc
ccwwx...x...x...xwwcc
cwwx.x.x.cFc.x.x.xwwc
cwx...x.cc9cc.x...xwc
+..x.x.cc.%.cc.x.x.ED
+...xt.D9%%%9+.tx..ED
+..x.x.cc.%.cc.x.x.ED
cwx...x.cc9cc.x...xwc
cwwx.x.x.cFc.x.x.xwwc
ccwwx...x...x...xwwcc
 ccwwx.x.xtx.x.xwwcc
  ccwwx...x...xwwcc
   ccwwx.x.x.xwwcc
    ccwwx...xwwcc
     ccwwHHHwwcc
      cccFFFccc
ENDMAP

NAME:   kennysheep_large_spiral
TAGS:   no_trap_gen no_item_gen
ORIENT: northwest
DEPTH:  D:15-, Depths, !D:$, !Depths:$
: ks_random_setup(_G, true)
MAP
ccccccccccccccccccccccccc
cwwwwwwwwwwwwwwwwwwwwwwwc
cwt9.................*twc
cw*xxxxxxxxxxxxxxxxxxx9wc
cw.xcccccccccccccccccx.wc
cw.xcwwwwwwwwwwwwwwwcx.wc
cw.xcwt9.........*twcx.wc
cw.xcw*xxxxxxxxxxx9wcx.wc
cw.xcw.xcccccccccx.wcx.wc
cw.xcw.xcwwwwwwwcx.wcx.wc
cw.xcw.xcwt888twcx.wcx.wc
cw.xcw.xcw.xxx|wcx.wcx.wc
cw.xcw.xcw.xcx|wcx.wcx.wc
cw.xcw.xcw.xcx|wcx.wcx.wc
cw.xcw.xcw.xcx|wcx.wcx.wc
cw.xcw.xcw.xcxwwcx.wcx.wc
cw.xcw.xcw.xcccccx.wcx.wc
cw.xcw.xcw9xxxxxxx*wcx.wc
cw.xcw.xcwt*.....9twcx.wc
cw.xcw.xcwwwwwwwwwwwcx.wc
cw.xcw.xcccccccccccccx.wc
cw.xcw*xxxxxxxxxxxxxxx*wc
cw.xcwt9.............9twc
cw.xcwwwwwwwwwwwwwwwwwwwc
cw@xccccccccccccccccccccc
ENDMAP

NAME:  kennysheep_potato
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
  cccFcccc
 ccwwHwwwccc
ccww....wwwcc
cww.......wwcc
cw....t....wwc
+....x.xxx..wcc
cw...x0%0x..wwc
cw...x%t%x...wc
cww..x0%0x...wc
ccw..xxx.x...ED
 cww....t....wc
 ccww.......wwc
  ccwww....wwcc
   cccwwwHwwcc
     ccccFccc
ENDMAP

NAME:  kennysheep_generic_room_2
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
cccccccFccccccc
cwwwwwwHwwwwwwc
cwwww.....wwwwc
cwww.......wwwc
cww....t....wwc
cw...xx.xx...wc
cw...x0%0x...wc
+...t.%t%.t..ED
cw...x0%0x...wc
cw...xx.xx...wc
cww....t....wwc
cwww.......wwwc
cwwww.....wwwwc
cwwwwwwHwwwwwwc
cccccccFccccccc
ENDMAP

NAME:  kennysheep_generic_room_3
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
  ccFFcc
 ccwHHwcc
ccww..wwcc
cwwxx.xwwc
+..x0t..ED
+...t0x.ED
cwwx.xxwwc
ccww..wwcc
 ccwHHwcc
  ccFFcc
ENDMAP

NAME:  kennysheep_generic_room_4
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFFcccc
cwwwHHwwwc
cwww..wwwc
cwwxx.xwwc
+..x0t..ED
+...t0x.ED
cwwx.xxwwc
cwww..wwwc
cwwwHHwwwc
ccccFFcccc
ENDMAP

NAME:  kennysheep_generic_room_5
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFFcccc
cwwwHHwwwc
cwxx..xxwc
cwxt0.txwc
+...%%0.ED
+..0%%..ED
cwxt.0txwc
cwxx..xxwc
cwwwHHwwwc
ccccFFcccc
ENDMAP

NAME:  kennysheep_generic_room_6
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFFcccc
cwwwHHwwwc
cw.%..0.wc
cw0txxt%wc
+..xxxx.ED
+..xxxx.ED
cw%txxt0wc
cw.0..%.wc
cwwwHHwwwc
ccccFFcccc
ENDMAP

NAME:  kennysheep_generic_room_7
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFFcccc
cwwwHHwwwc
cw....0.wc
cw0txx..wc
+..x%%x.ED
+..x%%x.ED
cw..xxt0wc
cw.0....wc
cwwwHHwwwc
ccccFFcccc
ENDMAP

NAME:  kennysheep_generic_room_8
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFcccc
cwwwHwwwc
cww...wwc
cw.0x%.wc
+..xxx.ED
cw.%x0.wc
cww...wwc
cwwwHwwwc
ccccFcccc
ENDMAP

NAME:  kennysheep_generic_room_9
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFcccc
cwwwHwwwc
cwxx.xxwc
cwx0x%xwc
+..xtx.ED
cwx%x0xwc
cwxx.xxwc
cwwwHwwwc
ccccFcccc
ENDMAP

NAME:  kennysheep_generic_room_10
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFcccc
cwwwHwwwc
cw..t..wc
cw.x0x.wc
+.t%x%tED
cw.x0x.wc
cw..t..wc
cwwwHwwwc
ccccFcccc
ENDMAP

NAME:  kennysheep_generic_room_11
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
ccccFcccc
cwwwHwwwc
cw..0..wc
cw.xxx.wc
+..txt.ED
cw.xxx.wc
cw..0..wc
cwwwHwwwc
ccccFcccc
ENDMAP

NAME:  kennysheep_generic_room_12
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
cccFFccc
cwwHHwwc
cwx.xxwc
+.x0t.ED
+..t0xED
cwxx.xwc
cwwHHwwc
cccFFccc
ENDMAP

NAME:  kennysheep_generic_room_13
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
cccFccc
cww.wwc
cw%x0wc
+.xtxED
cw0x%wc
cww.wwc
cccFccc
ENDMAP

NAME:  kennysheep_generic_room_14
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
cccFcc
cwwHwc
+.t0wc
cw0tED
cwHwwc
ccFccc
ENDMAP

NAME:  kennysheep_generic_room_15
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
cccFFccc
cwwHHwwc
cwt.0twc
+..xx.ED
+..xx.ED
cwt0.twc
cwwHHwwc
cccFFccc
ENDMAP

NAME:  kennysheep_generic_room_16
DEPTH: D:2-, Depths, !D:$, !Depths:$
SUBST: % = %.
SUBST: 0 = 0.
: ks_random_setup(_G)
MAP
cccccccFccccccc
cwwwwwwHwwwwwwc
cw...........wc
cw...........wc
cw...xxxxx...wc
cw...0%x%0...wc
cw.....x.....wc
+..xxxxtxxxx.ED
cw...0%x%0...wc
cw.....x.....wc
cw...xxxxx...wc
cw...........wc
cw...........wc
cwwwwwwHwwwwwwc
cccccccFccccccc
ENDMAP

NAME:  kennysheep_small_circle
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
: if crawl.coinflip() then
SUBST: * : %
SUBST: % = %$..
: else
SUBST: * : .
SUBST: % : {}OB
KFEAT: O = any shop
: end
SUBST: 0 = 0.
MAP
  ccDcc
 cwEwcc
ccww0wwcc
cwwt*twwc
FH0*%*0HF
cwwt*twwc
ccww0wwcc
 ccw.wcc
  cc+cc
ENDMAP

NAME:  kennysheep_small_square
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
: if crawl.coinflip() then
SUBST: * : %
SUBST: % = %$..
: else
SUBST: * : .
SUBST: % : {}O
KFEAT: O = any shop
: end
SUBST: 0 = 0.
MAP
ccccDcccc
cwwwEwwwc
cwt.0.twc
cw..*..wc
FH0*%*0HF
cw..*..wc
cwt.0.twc
cwww.wwwc
cccc+cccc
ENDMAP

NAME:   kennysheep_inner_square
DEPTH:  D:15-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
NSUBST: % : 1:|
SUBST:  % = *
SUBST:  9 = 9900.
MAP
   ccccDcccc
  ccwwwEwwwcc
 ccww..x..wwcc
ccww..xtx..wwcc
cww..x.x.x..wwc
cw..xtx.xtx..wc
cw.x.x9%9x.x.wc
FHxtx.%%%.xtxHF
cw.x.x9%9x.x.wc
cw..xtx.xtx..wc
cww..x.x.x..wwc
ccww..xtx..wwcc
 ccww..x..wwcc
  ccwww.wwwcc
   cccc+cccc
ENDMAP

NAME:  kennysheep_little_box
TAGS:  extra decor
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
KFEAT: O = any shop
SUBST: {={}OB....
MAP
cccDccc
cwwEwwc
cwt.twc
FH.{.HF
cwt.twc
cww.wwc
ccc+ccc
ENDMAP

NAME:   kennysheep_dice
SUBST:  % = %$.
SUBST:  0 = 0.
DEPTH:  D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
MAP
ccccccccDDcccccccc
cwwwwwwwEEwwwwwwwc
cw..............wc
cw.txxt....txxt.wc
cw.xxxx....xxxx.wc
cw.xxxx....xxxx.wc
cw.txxt....txxt.wc
cw.....0..0.....wc
FH......%%......HF
FH......%%......HF
cw.....0..0.....wc
cw.txxt....txxt.wc
cw.xxxx....xxxx.wc
cw.xxxx....xxxx.wc
cw.txxt....txxt.wc
cw..............wc
cwwwwwww..wwwwwwwc
cccccccc++cccccccc
ENDMAP

NAME:   kennysheep_two_boxes
DEPTH:  D:15-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
NSUBST: % : 1:|
SUBST:  % = *
SUBST:  9 = 9900.
MAP
ccccccDcccccc
ctwwwwEwwwwtc
cw.........wc
cw.xxx.xxx.wc
cw.xtwEwtx.wc
cw.xw9%9wx.wc
FH..H%t%H..HF
cw.xw9%9wx.wc
cw.xtw.wtx.wc
cw.xxx.xxx.wc
cw.........wc
ctwwww.wwwwtc
cccccc+cccccc
ENDMAP

NAME:  kennysheep_diamond
TAGS:  extra
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
: if crawl.coinflip() then
SUBST: * : %
SUBST: % = %$..
: else
SUBST: * : .
SUBST: % : {}OCB
KFEAT: O = any shop
: end
SUBST: 0 = 0.
MAP
       ccDcc
      ccwEwcc
     ccww.wwcc
    ccww.t.wwcc
   ccww.xxx.wwcc
  ccww.xxtxx.wwcc
 ccww.xxw.wxx.wwcc
ccww.xxw.0.wxx.wwcc
cww.xxw..*..wxx.wwc
FH.....0*%*0.....HF
cww.xxw..*..wxx.wwc
ccww.xxw.0.wxx.wwcc
 ccww.xxw.wxx.wwcc
  ccww.xxtxx.wwcc
   ccww.xxx.wwcc
    ccww.t.wwcc
     ccww.wwcc
      ccw.wcc
       cc+cc
ENDMAP

NAME:   kennysheep_bubble_arena
TAGS:   no_item_gen no_trap_gen
ORIENT: float
DEPTH:  D:15-, Depths, !D:$, !Depths:$
NSUBST: % = 5:|
SUBST:  % : %$
: ks_random_setup(_G, true)
MAP
    ccccccccc
   ccwwwwwwwcc
  ccww.....wwcc
 ccwwt.....twwcc
ccww.........wwcc
cww....xtx....wwc
cwt....8x8....twc
cw...x9x%x9x...wc
cw...tx%%%xt...wc
cw...x%x%x%x...wc
cwt....%x%....twc
cww....xtx....wwc
ccww.........wwcc
 ccwwt.....twwcc
  ccww.....wwcc
   ccw.....wcc
    cc+++++cc
   ccw.....wcc
  ccww.9.9.wwcc
  cww...x...wwc
  cwt..%x%..twc
  cw...xtx...wc
  cwt..%x%..twc
  cww...x...wwc
  ccww.....wwcc
   ccww...wwcc
    ccc+++ccc
     cc...cc
    ccw.9.wcc
    cww...wwc
    cwt...twc
    cww...wwc
    ccw...wcc
     cc...cc
      c+++c
ENDMAP

NAME:   kennysheep_windowed_goal
DEPTH:  D:4-, Depths, !D:$, !Depths:$
: if crawl.coinflip() then
SUBST:  O : 0
: else
KFEAT:  O = any shop
SUBST:  } : 0
: end
: if crawl.coinflip() then
SUBST:  w:.
: else
SUBST:  t:w
: end
: ks_random_setup(_G, true)
MAP
  ccccc
 ccwwwcc
ccw...wcc
+....}..m@
+..}O...m@
+....}..m@
ccw...wcc
 ccwwwcc
  ccccc
ENDMAP

NAME:  kennysheep_nondecorative_pillar
TAGS:  transparent
DEPTH: Depths:2-, !D:$, !Depths:$
: ks_random_setup(_G, true)
MAP
     ...xtx....
    .....x......
   ..............
  ..x..........x..
 ..xx..........xx..
....................
.......wwwwww.......
......wwccccww......
......wcc||ccw.....x
x.....wc*88*cw....xt
tx....wc*..*cw.....x
x.....wcc..ccw......
......wwc++cww......
.......ww..ww.......
....................
 ..xx..........xx..
  ..x..........x..
   ..............
    ......x.....
     ....xtx...
ENDMAP
